import QuasiBorelSpaces.Hom
import QuasiBorelSpaces.OmegaCompletePartialOrder.Basic
import QuasiBorelSpaces.OmegaQuasiBorelSpace
import QuasiBorelSpaces.Prod

/-!
# Exponentials for Ï‰-quasi-borel spaces

This file defines the function space `OmegaQuasiBorelHom X Y` (written
`X â†’Ï‰ğ’’ Y`) of Scott-continuous QBS morphisms. It proves that this space is
itself an Ï‰QBS.
-/

open QuasiBorelSpace
open OmegaQuasiBorelSpace
open OmegaCompletePartialOrder

/--
Exponential objects: functions that are both Scott-Continuous and Measurable (QBS Morphisms)
-/
structure OmegaQuasiBorelHom
    (X Y : Type*)
    [OmegaQuasiBorelSpace X] [OmegaQuasiBorelSpace Y] where
  private toFun : X â†’ Y
  private isHom' : IsHom toFun := by fun_prop
  private Ï‰ScottContinuous' : Ï‰ScottContinuous toFun := by fun_prop

@[inherit_doc] infixr:25 " â†’Ï‰ğ’’ " => OmegaQuasiBorelHom

namespace OmegaQuasiBorelHom

variable {X Y Z : Type*} [OmegaQuasiBorelSpace X] [OmegaQuasiBorelSpace Y] [OmegaQuasiBorelSpace Z]

instance : FunLike (X â†’Ï‰ğ’’ Y) X Y where
  coe f := (f.1 : X â†’ Y)
  coe_injective' f g h := by
    cases f
    cases g
    simp_all only

/-- A simps projection for function coercion. -/
def Simps.coe (f : X â†’Ï‰ğ’’ Y) : X â†’ Y := f

initialize_simps_projections OmegaQuasiBorelHom (toFun â†’ coe)

@[ext]
lemma ext {f g : X â†’Ï‰ğ’’ Y} (h : âˆ€ x, f x = g x) : f = g := DFunLike.ext f g h

/--
Copy of a `OmegaQuasiBorelHom` with a new `toFun` equal to the old one.
Useful to fix definitional equalities.
-/
protected def copy (f : X â†’Ï‰ğ’’ Y) (f' : X â†’ Y) (h : f' = â‡‘f) : X â†’Ï‰ğ’’ Y where
  toFun := f'
  isHom' := h.symm â–¸ f.isHom'
  Ï‰ScottContinuous' := h.symm â–¸ f.Ï‰ScottContinuous'

@[simp]
lemma coe_mk {f : X â†’ Y} (hfâ‚ : IsHom f) (hfâ‚‚ : Ï‰ScottContinuous f) : â‡‘(mk f hfâ‚ hfâ‚‚) = f := rfl

@[simp]
lemma eta (f : X â†’Ï‰ğ’’ Y) : mk f f.isHom' f.Ï‰ScottContinuous' = f := rfl

@[simp]
lemma toFun_eq_coe (f : X â†’Ï‰ğ’’ Y) : toFun f = â‡‘f := rfl

@[simp, fun_prop]
lemma isHom_coe (f : X â†’Ï‰ğ’’ Y) : IsHom (f : X â†’ Y) := f.2

@[simp, fun_prop]
lemma Ï‰ScottContinuous_coe (f : X â†’Ï‰ğ’’ Y) : Ï‰ScottContinuous (f : X â†’ Y) := f.3

@[simp]
lemma monotone_coe (f : X â†’Ï‰ğ’’ Y) : Monotone (f : X â†’ Y) := f.3.monotone

instance : PartialOrder (X â†’Ï‰ğ’’ Y) :=
  PartialOrder.lift DFunLike.coe DFunLike.coe_injective

/-- Converts an Ï‰QBS Hom to a Poset Hom. -/
@[simps, coe]
def toOrderHom (f : X â†’Ï‰ğ’’ Y) : X â†’o Y where
  toFun := f
  monotone' := f.monotone_coe

/-- Converts a Ï‰QBS Hom to an Ï‰CPO Hom. -/
@[simps, coe]
def toContinuousHom (f : X â†’Ï‰ğ’’ Y) : X â†’ğ’„ Y where
  toFun := f
  monotone' := f.monotone_coe
  map_Ï‰Sup' := f.Ï‰ScottContinuous_coe.map_Ï‰Sup

/-- Converts a Ï‰QBS Hom to a quasi-Borel Hom. -/
@[simps, coe]
def toQuasiBorelHom (f : X â†’Ï‰ğ’’ Y) : X â†’ğ’’ Y where
  toFun := f

/-- The Ï‰CPO structure on the exponential is the pointwise order. -/
@[simps!]
instance : OmegaCompletePartialOrder (X â†’Ï‰ğ’’ Y) :=
  OmegaCompletePartialOrder.lift
    âŸ¨DFunLike.coe, fun _ _ h â†¦ hâŸ©
    (fun c â†¦ {
      toFun := Ï‰Sup (c.map âŸ¨DFunLike.coe, fun _ _ h â†¦ hâŸ©)
      isHom' := by
        rw [isHom_def]
        intro Ï† hÏ†
        let c' : Chain (â„ â†’ Y) := {
          toFun x r := (c x) (Ï† r)
          monotone' i j h r := c.monotone h (Ï† r)
        }
        apply isHom_Ï‰Sup c' fun n â†¦ ?_
        simp only [Chain, OrderHom.coe_mk, c']
        fun_prop
      Ï‰ScottContinuous' := by
        let c' : Chain (X â†’ğ’„ Y) := {
          toFun n := (c n).toContinuousHom
          monotone' i j h := c.monotone h
        }
        change Ï‰ScottContinuous (DFunLike.coe (Ï‰Sup c'))
        apply ContinuousHom.Ï‰ScottContinuous
    })
    (fun _ _ h â†¦ h)
    (by simp only [OrderHom.coe_mk, coe_mk, implies_true])

/-- The QBS structure on the Ï‰Homs is identical to normal QBS Homs. -/
instance : QuasiBorelSpace (X â†’Ï‰ğ’’ Y) where
  IsVar Ï† := IsHom (fun x : â„ Ã— X â†¦ Ï† x.1 x.2)
  isVar_const f := by fun_prop
  isVar_comp hf hÏ† := by
    rw [â† isHom_iff_measurable] at hf
    fun_prop
  isVar_cases' {ix} {Ï†} hix hÏ† := by
    rw [â† isHom_iff_measurable] at hix
    let ix' := fun (p : â„ Ã— X) â†¦ ix p.1
    have hix' : IsHom ix' := by
      apply isHom_comp (hf := hix)
      exact Prod.isHom_fst
    let branches := fun n (p : â„ Ã— X) â†¦ (Ï† n p.1) p.2
    apply isHom_cases (ix := ix') (f := branches)
    Â· exact hix'
    Â· exact hÏ†

@[local simp]
lemma isHom_def (Ï† : â„ â†’ X â†’Ï‰ğ’’ Y) :
    IsHom Ï† â†” IsHom (fun x : â„ Ã— X â†¦ Ï† x.1 x.2) := by
  rw [â† isVar_iff_isHom]
  rfl

@[simp, fun_prop]
lemma isHom_eval : IsHom (fun p : (X â†’Ï‰ğ’’ Y) Ã— X â†¦ p.1 p.2) := by
  rw [QuasiBorelSpace.isHom_def]
  intro Ï† hÏ†
  have h_func : IsHom (fun r â†¦ (Ï† r).1) := isHom_comp Prod.isHom_fst hÏ†
  have h_arg  : IsHom (fun r â†¦ (Ï† r).2) := isHom_comp Prod.isHom_snd hÏ†
  rw [isHom_def] at h_func
  have h_input : IsHom (fun r : â„ â†¦ (r, (Ï† r).2)) := by
    apply Prod.isHom_mk
    Â· exact isHom_id
    Â· exact h_arg
  apply isHom_comp (hf := h_func) (hg := h_input)

@[fun_prop]
lemma isHom_eval'
    {f : X â†’ Y â†’Ï‰ğ’’ Z} (hf : IsHom f)
    {g : X â†’ Y} (hg : IsHom g)
    : IsHom (fun x â†¦ f x (g x)) := by
  apply isHom_comp' (f := fun x â†¦ x.1 x.2) (g := fun x â†¦ (f x, g x))
  Â· simp only [isHom_eval]
  Â· fun_prop

@[simp]
lemma isHom_iff (f : X â†’ Y â†’Ï‰ğ’’ Z) : IsHom f â†” IsHom (fun x : X Ã— Y â†¦ f x.1 x.2) := by
  apply Iff.intro
  Â· intro hf
    rw [QuasiBorelSpace.isHom_def]
    simp only [Prod.isHom_iff, and_imp]
    intro Ï† hÏ†â‚ hÏ†â‚‚
    fun_prop
  Â· intro hf
    rw [QuasiBorelSpace.isHom_def]
    intro Ï† hÏ†
    simp only [isHom_def]
    fun_prop

/-- The exponential object is an Ï‰QBS. -/
instance : OmegaQuasiBorelSpace (X â†’Ï‰ğ’’ Y) where
  isHom_Ï‰Sup' := by
    intro c hc
    rw [isHom_def]
    let c' : Chain (â„ Ã— X â†’ Y) := {
      toFun n x := c n x.1 x.2
      monotone' i j h x := c.monotone h x.1 x.2
    }
    apply isHom_Ï‰Sup c' fun n â†¦ ?_
    simp only [Chain, OrderHom.coe_mk, c']
    apply isHom_comp' (f := fun x : (X â†’Ï‰ğ’’ Y) Ã— X â†¦ x.1 x.2) (g := fun x : â„ Ã— X â†¦ (c n x.1, x.2))
    Â· apply isHom_eval
    Â· fun_prop

/-! ### Operations -/

/-- Identity `OmegaQuasiBorelHom`s. -/
@[simps]
def id : X â†’Ï‰ğ’’ X where
  toFun x := x

/-- Function composition for `OmegaQuasiBorelHom`s. -/
@[simps coe]
def comp (f : Y â†’Ï‰ğ’’ Z) (g : X â†’Ï‰ğ’’ Y) : X â†’Ï‰ğ’’ Z where
  toFun x := f (g x)

/-- Product construction as an `OmegaQuasiBorelHom`. -/
@[simps coe]
def Prod.mk (f : X â†’Ï‰ğ’’ Y) (g : X â†’Ï‰ğ’’ Z) : X â†’Ï‰ğ’’ Y Ã— Z where
  toFun x := (f x, g x)

/-- First product projection. -/
@[simps coe]
def Prod.fst : X Ã— Y â†’Ï‰ğ’’ X where
  toFun x := x.1

/-- Second product projection. -/
@[simps coe]
def Prod.snd : X Ã— Y â†’Ï‰ğ’’ Y where
  toFun x := x.2

/-- Currying for `OmegaQuasiBorelHom`s. -/
@[simps coe]
def curry (f : Z Ã— X â†’Ï‰ğ’’ Y) : Z â†’Ï‰ğ’’ (X â†’Ï‰ğ’’ Y) where
  toFun x := { toFun y := f (x, y) }
  -- TODO: figure out what fun_prop lemmas we need to prove these automatically.
  isHom' := by simp only [isHom_iff, coe_mk, Prod.mk.eta, isHom_coe]
  Ï‰ScottContinuous' := by
    rw [Ï‰ScottContinuous_iff_monotone_map_Ï‰Sup]
    refine âŸ¨fun x y h z â†¦ ?_, fun c â†¦ ?_âŸ©
    Â· simp only [coe_mk]
      apply f.monotone_coe
      simp only [Prod.mk_le_mk, h, le_refl, and_self]
    Â· ext x
      simp only [coe_mk, Ï‰Sup_coe]
      rw [(by simp only [Ï‰Sup_const] : x = Ï‰Sup (Chain.const x))]
      change f (Ï‰Sup (Chain.zip c (Chain.const x))) = _
      rw [f.Ï‰ScottContinuous_coe.map_Ï‰Sup]
      apply le_antisymm
      Â· simp only [
          Ï‰Sup_const, Ï‰Sup_le_iff, Chain.map_coe, OrderHom.coe_mk,
          Function.comp_apply, Chain.zip_coe, Chain.const_apply]
        intro i
        apply le_Ï‰Sup_of_le i
        simp only [
          Chain.map_coe, Pi.evalOrderHom_coe, OrderHom.coe_mk,
          Function.comp_apply, Function.eval, coe_mk, le_refl]
      Â· simp only [
          Ï‰Sup, Ï‰Sup_const, Ï‰Sup_le_iff, Chain.map_coe, Pi.evalOrderHom_coe,
          OrderHom.coe_mk, Function.comp_apply, Function.eval, coe_mk]
        intro i
        apply le_Ï‰Sup_of_le i
        simp only [
          Chain.map_coe, OrderHom.coe_mk, Function.comp_apply,
          Chain.zip_coe, Chain.const_apply, le_refl]

/-- Function application is an `OmegaQuasiBorelHom`. -/
@[simps coe]
def eval : (X â†’Ï‰ğ’’ Y) Ã— X â†’Ï‰ğ’’ Y where
  toFun x := x.1 x.2
  -- TODO: figure out what fun_prop lemmas we need to prove this automatically.
  Ï‰ScottContinuous' := by
    rw [Ï‰ScottContinuous_iff_monotone_map_Ï‰Sup]
    refine âŸ¨fun x y h â†¦ ?_, fun c â†¦ ?_âŸ©
    Â· simp only
      trans
      Â· apply h.1
      Â· apply y.1.monotone_coe
        apply h.2
    Â· simp only [Ï‰Sup, Prod.Ï‰SupImpl_fst, Prod.Ï‰SupImpl_snd, coe_mk]
      apply le_antisymm
      Â· simp only [
          Ï‰Sup_le_iff, Chain.map_coe, Pi.evalOrderHom_coe, OrderHom.coe_mk,
          Function.comp_apply, Function.eval, OrderHom.fst_coe]
        intro i
        rw [(c i).1.Ï‰ScottContinuous_coe.map_Ï‰Sup]
        simp only [
          Chain.map_coe, OrderHom.coe_mk, Function.comp_apply,
          Ï‰Sup_le_iff, OrderHom.snd_coe]
        intro j
        apply le_Ï‰Sup_of_le (i âŠ” j)
        simp only [Chain.map_coe, OrderHom.coe_mk, Function.comp_apply]
        trans
        Â· apply (c i).1.monotone_coe
          apply (c.monotone (by simp only [le_sup_right] : j â‰¤ i âŠ” j)).2
        Â· apply (c.monotone ?_).1
          simp only [le_sup_left]
      Â· simp only [Ï‰Sup_le_iff, Chain.map_coe, OrderHom.coe_mk, Function.comp_apply]
        intro i
        apply le_Ï‰Sup_of_le i
        simp only [
          Chain.map_coe, Pi.evalOrderHom_coe, OrderHom.coe_mk,
          Function.comp_apply, Function.eval, OrderHom.fst_coe]
        apply (c i).1.monotone_coe
        apply le_Ï‰Sup_of_le i
        simp only [Chain.map_coe, Function.comp_apply, OrderHom.snd_coe, le_refl]

/-- Uncurrying for `OmegaQuasiBorelHom`s. -/
@[simps!]
def uncurry (f : X â†’Ï‰ğ’’ Y â†’Ï‰ğ’’ Z) : X Ã— Y â†’Ï‰ğ’’ Z :=
  eval.comp (Prod.mk (comp f Prod.fst) Prod.snd)

@[simp]
lemma curry_uncurry (f : Z â†’Ï‰ğ’’ (X â†’Ï‰ğ’’ Y)) : curry (uncurry f) = f := rfl

@[simp]
lemma uncurry_curry (f : Z Ã— X â†’Ï‰ğ’’ Y) : uncurry (curry f) = f := rfl

end OmegaQuasiBorelHom
