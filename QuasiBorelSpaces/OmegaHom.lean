import QuasiBorelSpaces.Hom
import QuasiBorelSpaces.OmegaCompletePartialOrder.Basic
import QuasiBorelSpaces.OmegaQuasiBorelSpace
import QuasiBorelSpaces.Prod

/-!
# Exponentials for Ï‰-quasi-borel spaces

This file defines the function space `OmegaQuasiBorelHom X Y` (written
`X â†’Ï‰ğ’’ Y`) of Scott-continuous QBS morphisms. It proves that this space is
itself an Ï‰QBS.
-/

open QuasiBorelSpace
open OmegaQuasiBorelSpace
open OmegaCompletePartialOrder

/--
Exponential objects: functions that are both Scott-Continuous and Measurable (QBS Morphisms)
-/
structure OmegaQuasiBorelHom
    (X Y : Type*)
    [OmegaQuasiBorelSpace X] [OmegaQuasiBorelSpace Y] where
  private toFun : X â†’ Y
  private isHom' : IsHom toFun := by fun_prop
  private Ï‰ScottContinuous' : Ï‰ScottContinuous toFun := by fun_prop

@[inherit_doc] infixr:25 " â†’Ï‰ğ’’ " => OmegaQuasiBorelHom

namespace OmegaQuasiBorelHom

variable {X Y Z : Type*} [OmegaQuasiBorelSpace X] [OmegaQuasiBorelSpace Y] [OmegaQuasiBorelSpace Z]

instance : FunLike (X â†’Ï‰ğ’’ Y) X Y where
  coe f := (f.1 : X â†’ Y)
  coe_injective' f g h := by
    cases f
    cases g
    simp_all only

/-- A simps projection for function coercion. -/
def Simps.coe (f : X â†’Ï‰ğ’’ Y) : X â†’ Y := f

initialize_simps_projections OmegaQuasiBorelHom (toFun â†’ coe)

@[ext]
lemma ext {f g : X â†’Ï‰ğ’’ Y} (h : âˆ€ x, f x = g x) : f = g := DFunLike.ext f g h

/--
Copy of a `OmegaQuasiBorelHom` with a new `toFun` equal to the old one.
Useful to fix definitional equalities.
-/
protected def copy (f : X â†’Ï‰ğ’’ Y) (f' : X â†’ Y) (h : f' = â‡‘f) : X â†’Ï‰ğ’’ Y where
  toFun := f'
  isHom' := h.symm â–¸ f.isHom'
  Ï‰ScottContinuous' := h.symm â–¸ f.Ï‰ScottContinuous'

@[simp]
lemma coe_mk {f : X â†’ Y} (hfâ‚ : IsHom f) (hfâ‚‚ : Ï‰ScottContinuous f) : â‡‘(mk f hfâ‚ hfâ‚‚) = f := rfl

@[simp]
lemma eta (f : X â†’Ï‰ğ’’ Y) : mk f f.isHom' f.Ï‰ScottContinuous' = f := rfl

@[simp]
lemma toFun_eq_coe (f : X â†’Ï‰ğ’’ Y) : toFun f = â‡‘f := rfl

@[simp, fun_prop]
lemma isHom_coe (f : X â†’Ï‰ğ’’ Y) : IsHom (f : X â†’ Y) := f.2

@[simp, fun_prop]
lemma Ï‰ScottContinuous_coe (f : X â†’Ï‰ğ’’ Y) : Ï‰ScottContinuous (f : X â†’ Y) := f.3

@[simp]
lemma monotone_coe (f : X â†’Ï‰ğ’’ Y) : Monotone (f : X â†’ Y) := f.3.monotone

instance : PartialOrder (X â†’Ï‰ğ’’ Y) :=
  PartialOrder.lift DFunLike.coe DFunLike.coe_injective

/-- Converts an Ï‰QBS Hom to a Poset Hom. -/
@[simps, coe]
def toOrderHom (f : X â†’Ï‰ğ’’ Y) : X â†’o Y where
  toFun := f
  monotone' := f.monotone_coe

/-- Converts a Ï‰QBS Hom to an Ï‰CPO Hom. -/
@[simps, coe]
def toContinuousHom (f : X â†’Ï‰ğ’’ Y) : X â†’ğ’„ Y where
  toFun := f
  monotone' := f.monotone_coe
  map_Ï‰Sup' := f.Ï‰ScottContinuous_coe.map_Ï‰Sup

/-- Converts a Ï‰QBS Hom to a quasi-Borel Hom. -/
@[simps, coe]
def toQuasiBorelHom (f : X â†’Ï‰ğ’’ Y) : X â†’ğ’’ Y where
  toFun := f

/-- The Ï‰CPO structure on the exponential is the pointwise order. -/
@[simps!]
instance : OmegaCompletePartialOrder (X â†’Ï‰ğ’’ Y) :=
  OmegaCompletePartialOrder.lift
    âŸ¨DFunLike.coe, fun _ _ h â†¦ hâŸ©
    (fun c â†¦ {
      toFun := Ï‰Sup (c.map âŸ¨DFunLike.coe, fun _ _ h â†¦ hâŸ©)
      isHom' := by
        simp only [
          Ï‰Sup, Chain.isHom_iff, Chain.map_coe, Pi.evalOrderHom_coe, OrderHom.coe_mk,
          Function.comp_apply, Function.eval, isHom_coe, implies_true, isHom_Ï‰Sup']
      Ï‰ScottContinuous' := by
        let c' : Chain (X â†’ğ’„ Y) := {
          toFun n := (c n).toContinuousHom
          monotone' i j h := c.monotone h
        }
        change Ï‰ScottContinuous (DFunLike.coe (Ï‰Sup c'))
        apply ContinuousHom.Ï‰ScottContinuous
    })
    (fun _ _ h â†¦ h)
    (by simp only [OrderHom.coe_mk, coe_mk, implies_true])

/-- The QBS structure on the Ï‰Homs is identical to normal QBS Homs. -/
instance : QuasiBorelSpace (X â†’Ï‰ğ’’ Y) where
  IsVar Ï† := IsHom (fun x : â„ Ã— X â†¦ Ï† x.1 x.2)
  isVar_const f := by fun_prop
  isVar_comp hf hÏ† := by
    rw [â† isHom_iff_measurable] at hf
    fun_prop
  isVar_cases' {ix} {Ï†} hix hÏ† := by
    rw [â† isHom_iff_measurable] at hix
    let ix' := fun (p : â„ Ã— X) â†¦ ix p.1
    have hix' : IsHom ix' := by
      apply isHom_comp (hf := hix)
      exact Prod.isHom_fst
    let branches := fun n (p : â„ Ã— X) â†¦ (Ï† n p.1) p.2
    apply isHom_cases (ix := ix') (f := branches)
    Â· exact hix'
    Â· exact hÏ†

instance : MeasurableSpace (X â†’Ï‰ğ’’ Y) := toMeasurableSpace

@[local simp]
lemma isHom_def (Ï† : â„ â†’ X â†’Ï‰ğ’’ Y) :
    IsHom Ï† â†” IsHom (fun x : â„ Ã— X â†¦ Ï† x.1 x.2) := by
  rw [â† isVar_iff_isHom]
  rfl

@[simp]
lemma isHom_eval : IsHom (fun p : (X â†’Ï‰ğ’’ Y) Ã— X â†¦ p.1 p.2) := by
  rw [QuasiBorelSpace.isHom_def]
  intro Ï† hÏ†
  have h_func : IsHom (fun r â†¦ (Ï† r).1) := isHom_comp Prod.isHom_fst hÏ†
  have h_arg  : IsHom (fun r â†¦ (Ï† r).2) := isHom_comp Prod.isHom_snd hÏ†
  rw [isHom_def] at h_func
  have h_input : IsHom (fun r : â„ â†¦ (r, (Ï† r).2)) := by
    apply Prod.isHom_mk
    Â· exact isHom_id
    Â· exact h_arg
  apply isHom_comp (hf := h_func) (hg := h_input)

@[simp]
lemma Ï‰ScottContinuous_eval : Ï‰ScottContinuous (fun p : (X â†’Ï‰ğ’’ Y) Ã— X â†¦ p.1 p.2) := by
  rw [Ï‰ScottContinuous_iff_monotone_map_Ï‰Sup]
  refine âŸ¨fun x y âŸ¨hâ‚, hâ‚‚âŸ© â†¦ ?_, fun c â†¦ ?_âŸ©
  Â· trans
    Â· apply hâ‚
    Â· apply y.1.monotone_coe
      apply hâ‚‚
  Â· simp only [Prod.Ï‰Sup_fst, Prod.Ï‰Sup_snd, Ï‰Sup_coe]
    apply le_antisymm
    Â· simp only [
        Ï‰Sup, Ï‰Sup_le_iff, Chain.map_coe, Pi.evalOrderHom_coe, OrderHom.coe_mk,
        Function.comp_apply, Function.eval, OrderHom.fst_coe]
      intro i
      rw [(c i).1.Ï‰ScottContinuous_coe.map_Ï‰Sup]
      simp only [Ï‰Sup_le_iff, Chain.map_coe, OrderHom.coe_mk, Function.comp_apply, OrderHom.snd_coe]
      intro j
      apply le_Ï‰Sup_of_le (i âŠ” j)
      simp only [Chain.map_coe, OrderHom.coe_mk, Function.comp_apply]
      trans
      Â· apply (c.monotone (by grind : i â‰¤ i âŠ” j)).1
      Â· apply (c (i âŠ” j)).1.monotone_coe
        apply (c.monotone (by grind : j â‰¤ i âŠ” j)).2
    Â· simp only [Ï‰Sup_le_iff, Chain.map_coe, OrderHom.coe_mk, Function.comp_apply]
      intro i
      apply le_Ï‰Sup_of_le i
      simp only [
        Chain.map_coe, Pi.evalOrderHom_coe, OrderHom.coe_mk,
        Function.comp_apply, Function.eval, OrderHom.fst_coe]
      apply (c i).1.monotone_coe
      apply le_Ï‰Sup_of_le i
      simp only [Chain.map_coe, Function.comp_apply, OrderHom.snd_coe, le_refl]

omit [OmegaQuasiBorelSpace X] in
@[fun_prop]
lemma isHom_eval' [QuasiBorelSpace X]
    {f : X â†’ Y â†’Ï‰ğ’’ Z} (hf : IsHom f)
    {g : X â†’ Y} (hg : IsHom g)
    : IsHom (fun x â†¦ f x (g x)) := by
  apply isHom_comp' (f := fun x â†¦ x.1 x.2) (g := fun x â†¦ (f x, g x))
  Â· simp only [isHom_eval]
  Â· fun_prop

@[fun_prop]
lemma Ï‰ScottContinuous_eval'
    {f : X â†’ Y â†’Ï‰ğ’’ Z} (hf : Ï‰ScottContinuous f)
    {g : X â†’ Y} (hg : Ï‰ScottContinuous g)
    : Ï‰ScottContinuous (fun x â†¦ f x (g x)) := by
  apply Ï‰ScottContinuous.comp (g := fun x â†¦ x.1 x.2) (f := fun x â†¦ (f x, g x))
  Â· simp only [Ï‰ScottContinuous_eval]
  Â· fun_prop

omit [OmegaQuasiBorelSpace X] in
@[simp]
lemma isHom_iff
    [QuasiBorelSpace X] (f : X â†’ Y â†’Ï‰ğ’’ Z)
    : IsHom f â†” IsHom (fun x : X Ã— Y â†¦ f x.1 x.2) := by
  apply Iff.intro
  Â· intro hf
    rw [QuasiBorelSpace.isHom_def]
    simp only [Prod.isHom_iff, and_imp]
    intro Ï† hÏ†â‚ hÏ†â‚‚
    fun_prop
  Â· intro hf
    rw [QuasiBorelSpace.isHom_def]
    intro Ï† hÏ†
    simp only [isHom_def]
    fun_prop

@[fun_prop]
lemma isHom_mk
    {f : X â†’ Y â†’ Z}
    (hâ‚ : IsHom fun x : X Ã— Y â†¦ f x.1 x.2)
    (hâ‚‚ : âˆ€x, Ï‰ScottContinuous (f x))
    : IsHom fun x â†¦ mk (f x) (by fun_prop) (hâ‚‚ x) := by
  simp only [isHom_iff, coe_mk, hâ‚]

@[fun_prop]
lemma Ï‰ScottContinuous_mk
    {f : X â†’ Y â†’ Z}
    (hâ‚ : âˆ€ x, IsHom (f x))
    (hâ‚‚ : Ï‰ScottContinuous fun x : X Ã— Y â†¦ f x.1 x.2)
    : Ï‰ScottContinuous fun x â†¦ mk (f x) (hâ‚ x) (by fun_prop) := by
  rw [Ï‰ScottContinuous_iff_monotone_map_Ï‰Sup]
  refine âŸ¨fun x y h z â†¦ ?_, fun c â†¦ ?_âŸ©
  Â· have : (x, z) â‰¤ (y, z) := âŸ¨h, le_rflâŸ©
    exact hâ‚‚.monotone this
  Â· ext x
    simp only [coe_mk, Ï‰Sup]
    rw [(by simp only [Ï‰Sup_const] : x = Ï‰Sup (Chain.const x))]
    change f (Ï‰Sup (Chain.zip c (Chain.const x))).1 (Ï‰Sup (Chain.zip c (Chain.const x))).2 = _
    rw [hâ‚‚.map_Ï‰Sup]
    congr 1
    ext n
    simp only [
      Chain.map_coe, OrderHom.coe_mk, Function.comp_apply, Chain.zip_coe,
      Chain.const_apply, Ï‰Sup_const, Pi.evalOrderHom_coe, Function.eval, coe_mk]

/-- The exponential object is an Ï‰QBS. -/
instance : OmegaQuasiBorelSpace (X â†’Ï‰ğ’’ Y) where
  isHom_Ï‰Sup := by
    simp only [Ï‰Sup, isHom_iff, coe_mk]
    apply isHom_Ï‰Sup'
    simp only [
      Chain.isHom_iff, Chain.map_coe, Pi.evalOrderHom_coe,
      OrderHom.coe_mk, Function.comp_apply, Function.eval]
    intro i
    apply isHom_comp'
        (f := fun x : (X â†’Ï‰ğ’’ Y) Ã— X â†¦ x.1 x.2)
        (g := fun x : Chain (X â†’Ï‰ğ’’ Y) Ã— X â†¦ (x.1 i, x.2))
    Â· fun_prop
    Â· apply Prod.isHom_mk
      Â· apply isHom_comp' (Chain.isHom_apply i) Prod.isHom_fst
      Â· fun_prop

/-! ### Operations -/

/-- Identity `OmegaQuasiBorelHom`s. -/
@[simps]
def id : X â†’Ï‰ğ’’ X where
  toFun x := x

/-- Function composition for `OmegaQuasiBorelHom`s. -/
@[simps coe]
def comp (f : Y â†’Ï‰ğ’’ Z) (g : X â†’Ï‰ğ’’ Y) : X â†’Ï‰ğ’’ Z where
  toFun x := f (g x)

/-- Product construction as an `OmegaQuasiBorelHom`. -/
@[simps coe]
def Prod.mk (f : X â†’Ï‰ğ’’ Y) (g : X â†’Ï‰ğ’’ Z) : X â†’Ï‰ğ’’ Y Ã— Z where
  toFun x := (f x, g x)

/-- First product projection. -/
@[simps coe]
def Prod.fst : X Ã— Y â†’Ï‰ğ’’ X where
  toFun x := x.1

/-- Second product projection. -/
@[simps coe]
def Prod.snd : X Ã— Y â†’Ï‰ğ’’ Y where
  toFun x := x.2

/-- Currying for `OmegaQuasiBorelHom`s. -/
@[simps coe]
def curry (f : Z Ã— X â†’Ï‰ğ’’ Y) : Z â†’Ï‰ğ’’ (X â†’Ï‰ğ’’ Y) where
  toFun x := { toFun y := f (x, y) }

/-- Function application is an `OmegaQuasiBorelHom`. -/
@[simps coe]
def eval : (X â†’Ï‰ğ’’ Y) Ã— X â†’Ï‰ğ’’ Y where
  toFun x := x.1 x.2

/-- Uncurrying for `OmegaQuasiBorelHom`s. -/
@[simps!]
def uncurry (f : X â†’Ï‰ğ’’ Y â†’Ï‰ğ’’ Z) : X Ã— Y â†’Ï‰ğ’’ Z :=
  eval.comp (Prod.mk (comp f Prod.fst) Prod.snd)

@[simp]
lemma curry_uncurry (f : Z â†’Ï‰ğ’’ (X â†’Ï‰ğ’’ Y)) : curry (uncurry f) = f := rfl

@[simp]
lemma uncurry_curry (f : Z Ã— X â†’Ï‰ğ’’ Y) : uncurry (curry f) = f := rfl

end OmegaQuasiBorelHom
