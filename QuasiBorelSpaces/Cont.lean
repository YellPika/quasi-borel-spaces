import QuasiBorelSpaces.OmegaHom

open QuasiBorelSpace
open OmegaCompletePartialOrder

namespace OmegaQuasiBorelSpace

/-- The continuation monad in the category of `OmegaQuasiBorelSpace`s. -/
structure Cont (R A : Type*) [OmegaQuasiBorelSpace R] [OmegaQuasiBorelSpace A] where
  /-- The underlying morphism. -/
  apply : (A â†’Ï‰ğ’’ R) â†’Ï‰ğ’’ R

namespace Cont

variable {R A B : Type*} [OmegaQuasiBorelSpace R] [OmegaQuasiBorelSpace A]

@[ext]
lemma ext {x y : Cont R A} (h : x.apply = y.apply) : x = y := by
  cases x
  cases y
  simp_all only

instance : PartialOrder (Cont R A) :=
  PartialOrder.lift apply (by
    rintro âŸ¨xâŸ© âŸ¨yâŸ©
    simp only [mk.injEq, imp_self])

instance : OmegaCompletePartialOrder (Cont R A) := by
  refine OmegaCompletePartialOrder.lift âŸ¨apply, ?_âŸ© (fun c â†¦ âŸ¨Ï‰Sup (c.map âŸ¨apply, ?_âŸ©)âŸ©) ?_ ?_
  Â· rintro âŸ¨xâŸ© âŸ¨yâŸ©
    simp only [LE.le, imp_self]
  Â· rintro âŸ¨xâŸ© âŸ¨yâŸ©
    simp only [LE.le, imp_self]
  Â· intro âŸ¨xâŸ© âŸ¨yâŸ©
    simp only [LE.le, OrderHom.coe_mk, imp_self]
  Â· simp only [OrderHom.coe_mk, implies_true]

instance : QuasiBorelSpace (Cont R A) :=
  QuasiBorelSpace.lift apply

@[simp, local fun_prop]
lemma isHom_val : IsHom (apply (R := R) (A := A)) := by
  rw [â† isHom_to_lift]
  simp only [isHom_id']

@[fun_prop]
lemma isHom_val'
    [QuasiBorelSpace B] {f : B â†’ Cont R A} (hf : IsHom f)
    : IsHom (fun x â†¦ (f x).apply) := by
  fun_prop

@[simp, local fun_prop]
lemma isHom_mk : IsHom (mk (R := R) (A := A)) := by
  apply isHom_of_lift

@[fun_prop]
lemma isHom_mk'
    [QuasiBorelSpace B] {f : B â†’ (A â†’Ï‰ğ’’ R) â†’Ï‰ğ’’ R} (hf : IsHom f)
    : IsHom (fun x â†¦ mk (f x)) := by
  fun_prop

@[simp, local fun_prop]
lemma Ï‰ScottContinuous_mk : Ï‰ScottContinuous (mk (R := R) (A := A)) := by
  rw [Ï‰ScottContinuous_iff_monotone_map_Ï‰Sup]
  refine âŸ¨fun x y h k â†¦ ?_, fun c â†¦ ?_âŸ©
  Â· apply h
  Â· rfl

@[fun_prop]
lemma Ï‰ScottContinuous_mk'
     [OmegaCompletePartialOrder B] {f : B â†’ (A â†’Ï‰ğ’’ R) â†’Ï‰ğ’’ R} (hf : Ï‰ScottContinuous f)
     : Ï‰ScottContinuous (fun x â†¦ mk (f x)) := by
  fun_prop

@[simp, local fun_prop]
lemma Ï‰ScottContinuous_val : Ï‰ScottContinuous (apply (R := R) (A := A)) := by
  rw [Ï‰ScottContinuous_iff_monotone_map_Ï‰Sup]
  refine âŸ¨fun x y h k â†¦ ?_, fun c â†¦ ?_âŸ©
  Â· apply h
  Â· rfl

@[fun_prop]
lemma Ï‰ScottContinuous_val'
    [OmegaCompletePartialOrder B] {f : B â†’ Cont R A} (hf : Ï‰ScottContinuous f)
    : Ï‰ScottContinuous (fun x â†¦ (f x).apply) := by
  fun_prop

instance : OmegaQuasiBorelSpace (Cont R A) where
  isHom_Ï‰Sup := by
    change IsHom fun x â†¦ mk _
    apply isHom_comp' isHom_mk
    apply isHom_Ï‰Sup'
    simp only [
      Chain.isHom_iff, Chain.map_coe, OrderHom.coe_mk,
      Function.comp_apply, OmegaQuasiBorelHom.isHom_iff]
    intro i
    apply isHom_comp'
        (f := fun x : Cont R A Ã— _ â†¦ x.1.apply x.2)
        (g := fun x : Chain (Cont R A) Ã— _ â†¦ (x.1 i, x.2))
        (by fun_prop)
    apply Prod.isHom_mk
    Â· apply isHom_comp' (Chain.isHom_apply i) Prod.isHom_fst
    Â· apply Prod.isHom_snd

/-- The `unit` operator (i.e., pure values) for the continuation monad. -/
@[simps]
def unit : A â†’Ï‰ğ’’ Cont R A where
  toFun x := âŸ¨{ toFun k := k x }âŸ©

/-- The `bind` operator (i.e., sequential composition) for the continuation monad. -/
@[simps]
def bind [OmegaQuasiBorelSpace B] : (A â†’Ï‰ğ’’ Cont R B) â†’Ï‰ğ’’ (Cont R A â†’Ï‰ğ’’ Cont R B) where
  toFun f := { toFun x := âŸ¨{ toFun k := x.apply { toFun y := (f y).apply k } }âŸ© }

@[simp]
lemma bind_unit [OmegaQuasiBorelSpace B] (f : A â†’Ï‰ğ’’ Cont R B) (x : A) : bind f (unit x) = f x := rfl

@[simp]
lemma unit_bind : bind (unit (R := R) (A := A)) = .id := rfl

@[simp]
lemma bind_bind {C : Type*}
    [OmegaQuasiBorelSpace B] [OmegaQuasiBorelSpace C]
    (f : B â†’Ï‰ğ’’ Cont R C) (g : A â†’Ï‰ğ’’ Cont R B)
    : (bind f).comp (bind g) = bind ((bind f).comp g) :=
  rfl

end Cont

end OmegaQuasiBorelSpace
