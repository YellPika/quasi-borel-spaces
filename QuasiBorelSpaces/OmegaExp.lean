import QuasiBorelSpaces.Hom
import QuasiBorelSpaces.OmegaCompletePartialOrder.Basic
import QuasiBorelSpaces.OmegaCompletePartialOrder.Limit
import QuasiBorelSpaces.OmegaQuasiBorelSpace
import QuasiBorelSpaces.Prod

/-!
# Exponentials for Ï‰-quasi-borel spaces

This file defines the function space `OmegaHom X Y` (written `X â†’Ï‰ğ’’ Y`) of
Scott-continuous QBS morphisms. It proves that this space is itself an Ï‰QBS.
-/

namespace QuasiBorelSpaces

open QuasiBorelSpace
open OmegaCompletePartialOrder

universe u v w

/--
pointwise supremum of a chain of QBS morphisms is a QBS morphism
(also known as the "Compatibility Axiom" for the exponential to be an Ï‰QBS)
-/
lemma isHom_Ï‰Sup_of_chain
    {X : Type u} {Y : Type v}
    [QuasiBorelSpace X] [OmegaQuasiBorelSpace Y]
    (c : Chain (X â†’ Y)) (hc : âˆ€ n, IsHom (c n)) :
    IsHom (Ï‰Sup c) := by
  rw [QuasiBorelSpace.isHom_def]
  intro Î± hÎ±
  let comp : (X â†’ Y) â†’o (â„ â†’ Y) :=
    { toFun := fun f r => f (Î± r)
      monotone' := by intro f g h r; exact h (Î± r) }
  let c' : Chain (â„ â†’ Y) := c.map comp
  have hc' : âˆ€ n, IsHom (c' n) := by
    intro n
    exact isHom_comp (hf := hc n) (hg := hÎ±)
  have hSup := (OmegaQuasiBorelSpace.isHom_Ï‰Sup (Î± := Y) c' hc')
  have h_eval : Ï‰Sup c' = fun r => Ï‰Sup c (Î± r) := by
    funext r
    have hÏ‰ := OmegaCompletePartialOrder.Ï‰Sup_eval (c := c') (x := r)
    have hchain : c'.map (evalOrderHom r) = c.map (evalOrderHom (Î± r)) := by ext n; rfl
    simpa [hchain] using hÏ‰
  simpa [h_eval] using hSup

/--
the type of the Exponential Object:
Functions that are both Scott-Continuous and Measurable (QBS Morphisms)
-/
def OmegaHom (X : Type u) (Y : Type v)
    [OmegaQuasiBorelSpace X] [OmegaQuasiBorelSpace Y] :=
  { f : X â†’ğ’„ Y // IsHom (f : X â†’ Y) }

@[inherit_doc] infixr:25 " â†’Ï‰ğ’’ " => OmegaHom

namespace OmegaHom

variable {X : Type u} {Y : Type v} {Z : Type w}
variable [OmegaQuasiBorelSpace X] [OmegaQuasiBorelSpace Y] [OmegaQuasiBorelSpace Z]

instance : FunLike (X â†’Ï‰ğ’’ Y) X Y where
  coe f := (f.1 : X â†’ Y)
  coe_injective' f g h := by
    apply Subtype.ext
    apply ContinuousHom.ext
    intro x
    exact congrArg (fun k => k x) h

@[ext]
lemma ext {f g : X â†’Ï‰ğ’’ Y} (h : âˆ€ x, f x = g x) : f = g := by
  apply Subtype.ext
  apply ContinuousHom.ext
  intro x
  exact h x

@[simp, fun_prop]
lemma isHom_coe (f : X â†’Ï‰ğ’’ Y) : IsHom (f : X â†’ Y) := f.2

@[simp]
lemma monotone (f : X â†’Ï‰ğ’’ Y) : Monotone f := (f.1).monotone

/-- the Ï‰CPO structure on the exponential is the pointwise order -/
instance : OmegaCompletePartialOrder (X â†’Ï‰ğ’’ Y) :=
  OmegaCompletePartialOrder.subtype
    (p := fun f : X â†’ğ’„ Y => IsHom (f : X â†’ Y))
    (hp := by
      intro c hc
      have hc_hom : âˆ€ n, IsHom (c n : X â†’ Y) := by
        intro n
        exact hc (c n) âŸ¨n, rflâŸ©
      let c_raw : Chain (X â†’ Y) :=
        âŸ¨fun n x => c n x, by intro i j h x; exact c.monotone h xâŸ©
      have hSup := isHom_Ï‰Sup_of_chain c_raw hc_hom
      have h_eq : (Ï‰Sup c_raw : X â†’ Y) = (Ï‰Sup c : X â†’ğ’„ Y) := by
        funext x
        trans Ï‰Sup ((c.map ContinuousHom.toMono).map (OrderHom.apply x))
        Â· apply OmegaCompletePartialOrder.Ï‰Sup_eval
        Â· rfl
      rw [h_eq] at hSup
      exact hSup)

/-- the QBS structure on the exponential (the standard Function Space definition) -/
instance : QuasiBorelSpace (X â†’Ï‰ğ’’ Y) where
  IsVar Ï† := IsHom (fun x : â„ Ã— X â†¦ Ï† x.1 x.2)
  isVar_const f := by fun_prop
  isVar_comp hf hÏ† := by
    rw [â† isHom_iff_measurable] at hf
    fun_prop
  isVar_cases' {ix} {Ï†} hix hÏ† := by
    rw [â† isHom_iff_measurable] at hix
    let ix' := fun (p : â„ Ã— X) => ix p.1
    have hix' : IsHom ix' := by
      apply isHom_comp (hf := hix)
      exact Prod.isHom_fst
    let branches := fun n (p : â„ Ã— X) => (Ï† n p.1) p.2
    apply isHom_cases (ix := ix') (f := branches)
    Â· exact hix'
    Â· exact hÏ†

/-- uncurried random variables correspond to morphisms -/
@[local simp]
lemma isHom_def (Ï† : â„ â†’ X â†’Ï‰ğ’’ Y) :
    IsHom Ï† â†” IsHom (fun x : â„ Ã— X => Ï† x.1 x.2) := by
  rw [â† isVar_iff_isHom]
  rfl

@[simp, fun_prop]
lemma isHom_eval : IsHom (fun p : (X â†’Ï‰ğ’’ Y) Ã— X => p.1 p.2) := by
  rw [QuasiBorelSpace.isHom_def]
  intro Ï† hÏ†
  have h_func : IsHom (fun r => (Ï† r).1) := isHom_comp Prod.isHom_fst hÏ†
  have h_arg  : IsHom (fun r => (Ï† r).2) := isHom_comp Prod.isHom_snd hÏ†
  rw [isHom_def] at h_func
  have h_input : IsHom (fun r : â„ => (r, (Ï† r).2)) := by
    apply Prod.isHom_mk
    Â· exact isHom_id
    Â· exact h_arg
  apply isHom_comp (hf := h_func) (hg := h_input)

/-! ### OmegaQuasiBorelSpace Instance -/

/--
the exponential object is an Ï‰QBS, we must show that the Ï‰-supremum operation is measurable
-/
instance : OmegaQuasiBorelSpace (X â†’Ï‰ğ’’ Y) where
  isHom_Ï‰Sup := by
    intro c hc
    rw [isHom_def]
    let c_uncurry : Chain ((â„ Ã— X) â†’ Y) := {
      toFun := fun n p => (c n p.1) p.2
      monotone' := by
        intro i j h p
        exact (c.monotone h p.1) p.2
    }
    have hc_uncurry : âˆ€ n, IsHom (c_uncurry n) := by
      intro n
      specialize hc n
      rw [isHom_def] at hc
      exact hc
    have hSup := isHom_Ï‰Sup_of_chain c_uncurry hc_uncurry
    have eq : (fun p => (Ï‰Sup c p.1) p.2) = Ï‰Sup c_uncurry := by
      ext p
      simp only [c_uncurry]
      rw [OmegaCompletePartialOrder.Ï‰Sup_eval]
      rfl
    rw [eq]
    exact hSup

/-! ### Currying Operations -/

/--
currying map: `(Z Ã— X â†’ Y) â†’ (Z â†’ (X â†’ Y))`
constructed using explicit `ContinuousHom` records to match fields `monotone'` and `map_Ï‰Sup'`.
-/
def curry (f : Z Ã— X â†’Ï‰ğ’’ Y) : Z â†’Ï‰ğ’’ (X â†’Ï‰ğ’’ Y) :=
  âŸ¨{
    toFun := fun z => âŸ¨{
      toFun := fun x => f (z, x)
      monotone' := by
        intro x1 x2 h
        exact f.monotone âŸ¨le_rfl, hâŸ©
      map_Ï‰Sup' := by
        intro c
        let c_prod : Chain (Z Ã— X) := {
          toFun := fun n => (z, c n),
          monotone' := fun i j h => âŸ¨le_rfl, c.monotone hâŸ©
        }
        have hf := f.1.map_Ï‰Sup' c_prod
        convert hf
        change z = Ï‰Sup (c_prod.map OrderHom.fst)
        have : c_prod.map OrderHom.fst = Chain.const z := rfl
        rw [this, OmegaCompletePartialOrder.Ï‰Sup_const]
    }, by
      apply isHom_comp (hf := f.2)
      apply Prod.isHom_mk
      Â· apply isHom_const
      Â· apply isHom_id
    âŸ©
    monotone' := by
      intro z1 z2 h x
      exact f.monotone âŸ¨h, le_rflâŸ©
    map_Ï‰Sup' := by
      intro c
      apply OmegaHom.ext; intro x
      dsimp
      change f.1 (Ï‰Sup c, x) = _
      let c_prod : Chain (Z Ã— X) := {
        toFun := fun n => (c n, x),
        monotone' := fun i j h => âŸ¨c.monotone h, le_refl xâŸ©
      }
      have h := f.1.map_Ï‰Sup' c_prod
      have h_lhs : f.1 (Ï‰Sup c, x) = f.1 (Ï‰Sup c_prod) := by
        congr 1
        apply Prod.ext
        Â· rw [Prod.Ï‰Sup_fst]; rfl
        Â· rw [Prod.Ï‰Sup_snd]
          have : c_prod.map OrderHom.snd = Chain.const x := rfl
          rw [this, OmegaCompletePartialOrder.Ï‰Sup_const]
      rw [h_lhs]
      exact h
  }, by
    rw [QuasiBorelSpace.isHom_def]
    intro Ï† hÏ†
    rw [isHom_def]
    dsimp
    apply isHom_comp (hf := f.2)
    apply Prod.isHom_mk
    Â· apply isHom_comp (hf := hÏ†) (hg := Prod.isHom_fst)
    Â· exact Prod.isHom_snd
  âŸ©

/-- uncurrying map: `(Z â†’ (X â†’ Y)) â†’ (Z Ã— X â†’ Y)` -/
def uncurry (f : Z â†’Ï‰ğ’’ (X â†’Ï‰ğ’’ Y)) : Z Ã— X â†’Ï‰ğ’’ Y :=
  âŸ¨{
    toFun := fun p => f p.1 p.2
    monotone' := by
      intro p1 p2 h
      apply le_trans (f.monotone h.1 p1.2)
      apply (f p2.1).monotone h.2
    map_Ï‰Sup' := by
      intro c
      let c1 := c.map OrderHom.fst
      let c2 := c.map OrderHom.snd
      let chain_inner (n : â„•) : Chain Y := {
        toFun := fun m => f (c1 n) (c2 m)
        monotone' := by
          intro i j h
          apply (f (c1 n)).monotone
          apply c2.monotone h
      }
      let chain_outer : Chain Y := {
        toFun := fun n => Ï‰Sup (chain_inner n)
        monotone' := by
          intro i j h
          apply Ï‰Sup_le; intro m
          apply le_trans (b := f (c1 j) (c2 m))
          Â· apply f.monotone
            apply c1.monotone h
          Â· apply le_Ï‰Sup (chain_inner j) m
      }
      have h_lhs : (f (Ï‰Sup c).1) (Ï‰Sup c).2 = Ï‰Sup chain_outer := by
        have h1 : (Ï‰Sup c).1 = Ï‰Sup c1 := rfl
        have h2 : (Ï‰Sup c).2 = Ï‰Sup c2 := rfl
        rw [h1, h2]
        have hf_cont := f.1.map_Ï‰Sup' c1
        change (f.1.toFun (Ï‰Sup c1)) (Ï‰Sup c2) = _
        rw [hf_cont]
        have h_pointwise : âˆ€ x, (Ï‰Sup (c1.map f.1.toOrderHom)) x =
            Ï‰Sup { toFun := fun n => f (c1 n) x,
                   monotone' := fun i j h => f.monotone (c1.monotone h) x } := by
          intro x
          rfl
        rw [h_pointwise]
        congr; funext n
        have h_fn_cont := (f (c1 n)).1.map_Ï‰Sup' c2
        exact h_fn_cont
      rw [h_lhs]
      apply le_antisymm
      Â· apply Ï‰Sup_le; intro n
        apply Ï‰Sup_le; intro m
        let k := max n m
        apply le_trans (b := f (c1 k) (c2 k))
        Â· apply le_trans (b := f (c1 n) (c2 k))
          Â· apply (f (c1 n)).monotone
            apply c2.monotone
            apply le_max_right
          Â· apply f.monotone
            apply c1.monotone
            apply le_max_left
        Â· convert le_Ï‰Sup _ k; rfl
      Â· apply Ï‰Sup_le; intro k
        apply le_trans (b := Ï‰Sup (chain_inner k))
        Â· apply le_Ï‰Sup (chain_inner k) k
        Â· apply le_Ï‰Sup chain_outer k
  }, by
    change IsHom (fun p : Z Ã— X => (f p.1) p.2)
    refine isHom_comp (hf := isHom_eval) (g := fun p : Z Ã— X => (f p.1, p.2)) ?_
    apply Prod.isHom_mk
    Â· apply isHom_comp (hf := f.2) (hg := Prod.isHom_fst)
    Â· exact Prod.isHom_snd
  âŸ©

@[simp]
lemma curry_uncurry (f : Z â†’Ï‰ğ’’ (X â†’Ï‰ğ’’ Y)) : curry (uncurry f) = f := by
  ext; rfl

@[simp]
lemma uncurry_curry (f : Z Ã— X â†’Ï‰ğ’’ Y) : uncurry (curry f) = f := by
  ext; rfl

end OmegaHom

end QuasiBorelSpaces
