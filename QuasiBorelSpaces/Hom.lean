import QuasiBorelSpaces.Prod

open QuasiBorelSpace

/-- The type of morphisms between `QuasiBorelSpace`s. -/
structure QuasiBorelHom (A B : Type*) [QuasiBorelSpace A] [QuasiBorelSpace B] where
  /-- The underlying function. -/
  toFun : A â†’ B
  /-- The underlying function is a morphism. -/
  private property : IsHom toFun := by fun_prop

namespace QuasiBorelHom

variable {A B C : Type*} [QuasiBorelSpace A] [QuasiBorelSpace B] [QuasiBorelSpace C]

@[inherit_doc]
infixr:25 " â†’ğ’’ " => QuasiBorelHom

instance : FunLike (A â†’ğ’’ B) A B where
  coe := toFun
  coe_injective' f g := by
    cases f
    cases g
    simp only [mk.injEq, imp_self]

/-- A simps projection for function coercion. -/
def Simps.coe (f : A â†’ğ’’ B) : A â†’ B := f

initialize_simps_projections QuasiBorelHom (toFun â†’ coe)

@[ext]
lemma ext {f g : A â†’ğ’’ B} (h : âˆ€ x, f x = g x) : f = g := DFunLike.ext f g h

/--
Copy of a `QuasiBorelHom` with a new `toFun` equal to the old one.
Useful to fix definitional equalities.
-/
protected def copy (f : A â†’ğ’’ B) (f' : A â†’ B) (h : f' = â‡‘f) : A â†’ğ’’ B where
  toFun := f'
  property := h.symm â–¸ f.property

@[simp]
lemma coe_mk {f : A â†’ B} (hf : IsHom f) : â‡‘(mk f hf) = f := rfl

@[simp]
lemma toFun_eq_coe (f : A â†’ğ’’ B) : toFun f = â‡‘f := rfl

@[simp, fun_prop]
lemma isHom_coe (f : A â†’ğ’’ B) : IsHom â‡‘f := f.property

@[simp]
lemma isVar_coe (f : â„ â†’ğ’’ A) : IsVar â‡‘f := by
  have := f.isHom_coe
  simp only [isHom_iff_isVar] at this
  exact this

@[simps IsVar]
instance : QuasiBorelSpace (QuasiBorelHom A B) where
  IsVar Ï† := IsHom (fun x : â„ Ã— A â†¦ Ï† x.1 x.2)
  isVar_const f := by
    intro Ï† hÏ†
    apply f.property
    apply hÏ†.2
  isVar_comp {f} {Ï†} hf hÏ† Ïˆ hÏˆ := by
    dsimp only
    apply hÏ† (Ï† := fun _ â†¦ (_, _))
    simp only [Prod.IsVar_def, Real.IsVar_def] at âŠ¢
    exact âŸ¨Measurable.comp hf hÏˆ.1, hÏˆ.2âŸ©
  isVar_cases' {ix} {Ï†} hix hÏ† Ïˆ hÏˆ := by
    apply isVar_cases' (ix := fun r â†¦ ix ((Ïˆ r).1)) (Ï† := fun n r â†¦ (Ï† n ((Ïˆ r).1)).1 (Ïˆ r).2)
    Â· exact Measurable.comp hix hÏˆ.1
    Â· intro n
      apply hÏ†
      exact hÏˆ

@[simp]
lemma isHom_iff (f : A â†’ B â†’ğ’’ C) : IsHom f â†” IsHom (fun x : A Ã— B â†¦ f x.1 x.2) := by
  apply Iff.intro
  Â· intro hf Ï† âŸ¨hÏ†â‚, hÏ†â‚‚âŸ©
    apply @hf (fun r â†¦ (Ï† r).1) hÏ†â‚ (fun r â†¦ (r, (Ï† r).2)) âŸ¨measurable_id, hÏ†â‚‚âŸ©
  Â· intro hf Ï† hÏ† Ïˆ âŸ¨hÏˆâ‚, hÏˆâ‚‚âŸ©
    refine hf (Ï† := fun r â†¦ (Ï† (Ïˆ r).1, (Ïˆ r).2)) âŸ¨isVar_comp (f := fun r â†¦ (Ïˆ r).1) hÏˆâ‚ hÏ†, hÏˆâ‚‚âŸ©

@[fun_prop]
lemma isHom_mk
    {f : A â†’ B â†’ C} (hf : IsHom (fun x : A Ã— B â†¦ f x.1 x.2))
    : IsHom (fun x â†¦ mk (f x) (by fun_prop)) := by
  simp only [isHom_iff, coe_mk, hf]

@[simp]
lemma isHom_eval : IsHom (fun p : (A â†’ğ’’ B) Ã— A => p.1 p.2) := by
  intro Ï† âŸ¨hÏ†â‚, hÏ†â‚‚âŸ©
  simp only [IsVar_def] at hÏ†â‚
  apply @hÏ†â‚ fun r â†¦ (r, (Ï† r).2)
  simp only [Prod.IsVar_def, Real.IsVar_def, measurable_id', true_and]
  exact hÏ†â‚‚

@[fun_prop]
lemma isHom_eval'
    {f : A â†’ B â†’ğ’’ C} (hf : IsHom f)
    {g : A â†’ B} (hg : IsHom g)
    : IsHom (fun x â†¦ f x (g x)) := by
  apply isHom_comp' (f := fun x â†¦ x.1 x.2) (g := fun x â†¦ (f x, g x))
  Â· simp only [isHom_eval]
  Â· fun_prop

/-- Currying for `QuasiBorelHom`s. -/
@[simps coe]
def curry (f : A Ã— B â†’ğ’’ C) : A â†’ğ’’ B â†’ğ’’ C where
  toFun x := { toFun y := f (x, y) }
  property := by
    simp only [isHom_iff, coe_mk, Prod.mk.eta]
    fun_prop

/-- Uncurrying for `QuasiBorelHom`s. -/
@[simps coe]
def uncurry (f : A â†’ğ’’ B â†’ğ’’ C) : A Ã— B â†’ğ’’ C where
  toFun x := f x.1 x.2

@[simp]
lemma curry_uncurry (f : A â†’ğ’’ B â†’ğ’’ C) : curry (uncurry f) = f := rfl

@[simp]
lemma uncurry_curry (f : A Ã— B â†’ğ’’ C) : uncurry (curry f) = f := rfl

end QuasiBorelHom
