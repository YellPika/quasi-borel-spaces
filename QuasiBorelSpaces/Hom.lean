import QuasiBorelSpaces.Nat
import QuasiBorelSpaces.Prod

/-!
# Exponentials of Quasi-Borel Spaces

This file defines the exponential object in the category of quasi-borel spaces.

See [HeunenKSY17], Proposition 18.
-/

open QuasiBorelSpace

/-- The type of morphisms between `QuasiBorelSpace`s. -/
structure QuasiBorelHom (A B : Type*) [QuasiBorelSpace A] [QuasiBorelSpace B] where
  /-- The underlying function. -/
  toFun : A â†’ B
  /-- The underlying function is a morphism. -/
  private property : IsHom toFun := by fun_prop

namespace QuasiBorelHom

variable {A B C : Type*} [QuasiBorelSpace A] [QuasiBorelSpace B] [QuasiBorelSpace C]

@[inherit_doc]
infixr:25 " â†’ğ’’ " => QuasiBorelHom

instance : FunLike (A â†’ğ’’ B) A B where
  coe := toFun
  coe_injective' f g := by
    cases f
    cases g
    simp only [mk.injEq, imp_self]

/-- A simps projection for function coercion. -/
def Simps.coe (f : A â†’ğ’’ B) : A â†’ B := f

initialize_simps_projections QuasiBorelHom (toFun â†’ coe)

@[ext]
lemma ext {f g : A â†’ğ’’ B} (h : âˆ€ x, f x = g x) : f = g := DFunLike.ext f g h

/--
Copy of a `QuasiBorelHom` with a new `toFun` equal to the old one.
Useful to fix definitional equalities.
-/
protected def copy (f : A â†’ğ’’ B) (f' : A â†’ B) (h : f' = â‡‘f) : A â†’ğ’’ B where
  toFun := f'
  property := h.symm â–¸ f.property

@[simp]
lemma coe_mk {f : A â†’ B} (hf : IsHom f) : â‡‘(mk f hf) = f := rfl

@[simp]
lemma eta (f : A â†’ğ’’ B) : mk f f.property = f := rfl

@[simp]
lemma toFun_eq_coe (f : A â†’ğ’’ B) : toFun f = â‡‘f := rfl

@[simp, fun_prop]
lemma isHom_coe (f : A â†’ğ’’ B) : IsHom â‡‘f := f.property

instance : QuasiBorelSpace (A â†’ğ’’ B) where
  IsVar Ï† := IsHom (fun x : â„ Ã— A â†¦ Ï† x.1 x.2)
  isVar_const f := by fun_prop
  isVar_comp hf hÏ† := by
    rw [â†isHom_iff_measurable] at hf
    fun_prop
  isVar_cases' {ix} {Ï†} hix hÏ† := by
    rw [â†isHom_iff_measurable] at hix
    apply isHom_cases (f := fun n (x : _ Ã— _) â†¦ (Ï† n x.1) x.2)
    Â· fun_prop
    Â· fun_prop

@[local simp]
lemma isHom_def (Ï† : â„ â†’ A â†’ğ’’ B) : IsHom Ï† â†” IsHom (fun x : â„ Ã— A â†¦ Ï† x.1 x.2) := by
  rw [â†isVar_iff_isHom]
  rfl

@[fun_prop, simp]
lemma isHom_eval : IsHom (fun p : (A â†’ğ’’ B) Ã— A => p.1 p.2) := by
  rw [QuasiBorelSpace.isHom_def]
  simp only [Prod.isHom_iff, isHom_def, and_imp]
  intro Ï† hÏ†â‚ hÏ†â‚‚
  apply @hÏ†â‚ fun r â†¦ (r, (Ï† r).2)
  simp only [Prod.isHom_iff, isHom_id', true_and]
  exact hÏ†â‚‚

@[fun_prop]
lemma isHom_eval'
    {f : A â†’ B â†’ğ’’ C} (hf : IsHom f)
    {g : A â†’ B} (hg : IsHom g)
    : IsHom (fun x â†¦ f x (g x)) := by
  apply isHom_comp' (f := fun x â†¦ x.1 x.2) (g := fun x â†¦ (f x, g x))
  Â· simp only [isHom_eval]
  Â· fun_prop

@[simp]
lemma isHom_iff (f : A â†’ B â†’ğ’’ C) : IsHom f â†” IsHom (fun x : A Ã— B â†¦ f x.1 x.2) := by
  apply Iff.intro
  Â· intro hf
    rw [QuasiBorelSpace.isHom_def]
    simp only [Prod.isHom_iff, and_imp]
    intro Ï† hÏ†â‚ hÏ†â‚‚
    fun_prop
  Â· intro hf
    rw [QuasiBorelSpace.isHom_def]
    intro Ï† hÏ†
    simp only [isHom_def]
    fun_prop

@[fun_prop]
lemma isHom_mk
    {f : A â†’ B â†’ C} (hf : IsHom (fun x : A Ã— B â†¦ f x.1 x.2))
    : IsHom (fun x â†¦ mk (f x) (by fun_prop)) := by
  simp only [isHom_iff, coe_mk, hf]

/-- Currying for `QuasiBorelHom`s. -/
@[simps coe]
def curry (f : A Ã— B â†’ğ’’ C) : A â†’ğ’’ B â†’ğ’’ C where
  toFun x := { toFun y := f (x, y) }
  property := by
    simp only [isHom_iff, coe_mk, Prod.mk.eta]
    fun_prop

/-- Uncurrying for `QuasiBorelHom`s. -/
@[simps coe]
def uncurry (f : A â†’ğ’’ B â†’ğ’’ C) : A Ã— B â†’ğ’’ C where
  toFun x := f x.1 x.2

@[simp]
lemma curry_uncurry (f : A â†’ğ’’ B â†’ğ’’ C) : curry (uncurry f) = f := rfl

@[simp]
lemma uncurry_curry (f : A Ã— B â†’ğ’’ C) : uncurry (curry f) = f := rfl

end QuasiBorelHom
