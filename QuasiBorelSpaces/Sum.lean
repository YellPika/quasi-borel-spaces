import QuasiBorelSpaces.Lift
import QuasiBorelSpaces.Sigma
import QuasiBorelSpaces.Hom

/-!
# Binary Coproducts of Quasi-Borel Spaces

This file defines binary coproducts of quasi-borel spaces by giving a
`QuasiBorelSpace` instance for the `¬∑ ‚äï ¬∑` type.

See [HeunenKSY17], Proposition 17.
-/

namespace QuasiBorelSpace.Sum

universe u v

variable
  {A : Type*} [QuasiBorelSpace A]
  {B : Type*} [QuasiBorelSpace B]
  {C : Type*} [QuasiBorelSpace C]
  {D : Type*} [QuasiBorelSpace D]
  {E : Type*} [QuasiBorelSpace E]

/--
We derive the `QuasiBorelSpace` instance for `A ‚äï B` via `Sigma (Encoding A B)`.
-/
def Encoding (A : Type u) (B : Type v) : Bool ‚Üí Type (max u v)
  | true => ULift A
  | false => ULift B

namespace Encoding

/-- The encoded version of `Sum.inl`. -/
def inl (x : A) : Sigma (Encoding A B) := ‚ü®true, ‚ü®x‚ü©‚ü©

/-- The encoded version of `Sum.inr`. -/
def inr (x : B) : Sigma (Encoding A B) := ‚ü®false, ‚ü®x‚ü©‚ü©

/-- The encoded version of `Sum.elim`. -/
def elim (f : A ‚Üí C) (g : B ‚Üí C) : Sigma (Encoding A B) ‚Üí C
  | ‚ü®true, x‚ü© => f x.down
  | ‚ü®false, x‚ü© => g x.down

instance {b : Bool} : QuasiBorelSpace (Encoding A B b) := by
  cases b <;>
  ¬∑ dsimp only [Encoding]
    infer_instance

@[fun_prop]
lemma isHom_inl : IsHom (inl (A := A) (B := B)) := by
  unfold inl
  fun_prop

@[fun_prop]
lemma isHom_inr : IsHom (inr (A := A) (B := B)) := by
  unfold inr
  fun_prop

@[fun_prop]
lemma isHom_elim {f : A ‚Üí C} (hf : IsHom f) {g : B ‚Üí C} (hg : IsHom g) : IsHom (elim f g) := by
  apply Sigma.isHom_elim fun b ‚Ü¶ ?_
  cases b <;>
  ¬∑ simp only [elim]
    fun_prop

end Encoding

/-- Encodes an `A ‚äï B` as a `Sigma (Encoding A B)`. -/
def encode : A ‚äï B ‚Üí Sigma (Encoding A B) :=
  Sum.elim Encoding.inl Encoding.inr

instance : QuasiBorelSpace (A ‚äï B) := lift encode

@[fun_prop]
lemma isHom_encode : IsHom (encode (A := A) (B := B)) := by
  apply isHom_of_lift

@[simp]
lemma isHom_inl : IsHom (Sum.inl : A ‚Üí A ‚äï B) := by
  simp only [isHom_to_lift, encode, Sum.elim_inl]
  fun_prop

@[fun_prop]
lemma isHom_inl' {f : A ‚Üí B} (hf : IsHom f) : IsHom (fun x ‚Ü¶ Sum.inl (f x) : A ‚Üí B ‚äï C) :=
  isHom_comp isHom_inl hf

@[simp]
lemma isHom_inr : IsHom (Sum.inr : B ‚Üí A ‚äï B) := by
  simp only [isHom_to_lift, encode, Sum.elim_inr]
  fun_prop

@[fun_prop]
lemma isHom_inr' {f : A ‚Üí C} (hf : IsHom f) : IsHom (fun x ‚Ü¶ Sum.inr (f x) : A ‚Üí B ‚äï C) :=
  isHom_comp isHom_inr hf

@[local fun_prop]
lemma isHom_elim
    {f : A ‚Üí C} (hf : IsHom f)
    {g : B ‚Üí C} (hg : IsHom g)
    : IsHom (Sum.elim f g) := by
  have : Sum.elim f g = fun x ‚Ü¶ Encoding.elim f g (encode x) := by
    ext x
    cases x <;> rfl
  rw [this]
  fun_prop

@[fun_prop]
lemma isHom_elim'
    {f : A ‚Üí B ‚Üí D} (hf : IsHom (fun x : A √ó B ‚Ü¶ f x.1 x.2))
    {g : A ‚Üí C ‚Üí D} (hg : IsHom (fun x : A √ó C ‚Ü¶ g x.1 x.2))
    {h : A ‚Üí B ‚äï C} (hh : IsHom h)
    : IsHom (fun x ‚Ü¶ Sum.elim (f x) (g x) (h x)) := by
  have {x}
      : Sum.elim (f x) (g x) (h x)
      = Sum.elim (Œ≥ := A ‚Üíùíí D) (fun x ‚Ü¶ .mk (f ¬∑ x)) (fun x ‚Ü¶ .mk (g ¬∑ x)) (h x) x := by
    cases h x <;> rfl
  simp only [this]
  fun_prop

@[fun_prop]
lemma isHom_map
    {f : A ‚Üí B ‚Üí D} (hf : IsHom fun x : A √ó B ‚Ü¶ f x.1 x.2)
    {g : A ‚Üí C ‚Üí E} (hg : IsHom fun x : A √ó C ‚Ü¶ g x.1 x.2)
    {h : A ‚Üí B ‚äï C} (hh : IsHom h)
    : IsHom (fun x ‚Ü¶ Sum.map (f x) (g x) (h x)) := by
  change IsHom fun x ‚Ü¶ Sum.elim (Sum.inl ‚àò f x) (Sum.inr ‚àò g x) (h x)
  fun_prop

end QuasiBorelSpace.Sum
