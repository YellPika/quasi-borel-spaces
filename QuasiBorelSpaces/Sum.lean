import QuasiBorelSpaces.Bool
import QuasiBorelSpaces.Lift
import QuasiBorelSpaces.Sigma
import QuasiBorelSpaces.Hom

/-!
# Binary Coproducts of Quasi-Borel Spaces

This file defines binary coproducts of quasi-borel spaces by giving a
`QuasiBorelSpace` instance for the `Â· âŠ• Â·` type.

See [HeunenKSY17], Proposition 17.
-/

namespace QuasiBorelSpace.Sum

universe u v

variable
  {A : Type*} [QuasiBorelSpace A]
  {B : Type*} [QuasiBorelSpace B]
  {C : Type*} [QuasiBorelSpace C]
  {D : Type*} [QuasiBorelSpace D]
  {E : Type*} [QuasiBorelSpace E]

/--
We derive the `QuasiBorelSpace` instance for `A âŠ• B` via `Sigma (Encoding A B)`.
-/
def Encoding (A : Type u) (B : Type v) : Bool â†’ Type (max u v)
  | true => ULift A
  | false => ULift B

namespace Encoding

/-- The encoded version of `Sum.inl`. -/
def inl (x : A) : Sigma (Encoding A B) := âŸ¨true, âŸ¨xâŸ©âŸ©

/-- The encoded version of `Sum.inr`. -/
def inr (x : B) : Sigma (Encoding A B) := âŸ¨false, âŸ¨xâŸ©âŸ©

/-- The encoded version of `Sum.elim`. -/
def elim (f : A â†’ C) (g : B â†’ C) : Sigma (Encoding A B) â†’ C
  | âŸ¨true, xâŸ© => f x.down
  | âŸ¨false, xâŸ© => g x.down

instance {b : Bool} : QuasiBorelSpace (Encoding A B b) := by
  cases b <;>
  Â· dsimp only [Encoding]
    infer_instance

@[fun_prop]
lemma isHom_inl : IsHom (inl (A := A) (B := B)) := by
  unfold inl
  fun_prop

@[fun_prop]
lemma isHom_inr : IsHom (inr (A := A) (B := B)) := by
  unfold inr
  fun_prop

@[fun_prop]
lemma isHom_elim {f : A â†’ C} (hf : IsHom f) {g : B â†’ C} (hg : IsHom g) : IsHom (elim f g) := by
  apply Sigma.isHom_elim fun b â†¦ ?_
  cases b <;>
  Â· simp only [elim]
    fun_prop

end Encoding

/-- Encodes an `A âŠ• B` as a `Sigma (Encoding A B)`. -/
def encode : A âŠ• B â†’ Sigma (Encoding A B) :=
  Sum.elim Encoding.inl Encoding.inr

instance : QuasiBorelSpace (A âŠ• B) := lift encode

@[fun_prop]
lemma isHom_encode : IsHom (encode (A := A) (B := B)) := by
  apply isHom_of_lift

@[simp]
lemma isHom_inl : IsHom (Sum.inl : A â†’ A âŠ• B) := by
  simp only [isHom_to_lift, encode, Sum.elim_inl]
  fun_prop

@[fun_prop]
lemma isHom_inl' {f : A â†’ B} (hf : IsHom f) : IsHom (fun x â†¦ Sum.inl (f x) : A â†’ B âŠ• C) :=
  isHom_comp isHom_inl hf

@[simp]
lemma isHom_inr : IsHom (Sum.inr : B â†’ A âŠ• B) := by
  simp only [isHom_to_lift, encode, Sum.elim_inr]
  fun_prop

@[fun_prop]
lemma isHom_inr' {f : A â†’ C} (hf : IsHom f) : IsHom (fun x â†¦ Sum.inr (f x) : A â†’ B âŠ• C) :=
  isHom_comp isHom_inr hf

@[local fun_prop]
lemma isHom_elim
    {f : A â†’ C} (hf : IsHom f)
    {g : B â†’ C} (hg : IsHom g)
    : IsHom (Sum.elim f g) := by
  have : Sum.elim f g = fun x â†¦ Encoding.elim f g (encode x) := by
    ext x
    cases x <;> rfl
  rw [this]
  fun_prop

@[fun_prop]
lemma isHom_elim'
    {f : A â†’ B â†’ D} (hf : IsHom (fun x : A Ã— B â†¦ f x.1 x.2))
    {g : A â†’ C â†’ D} (hg : IsHom (fun x : A Ã— C â†¦ g x.1 x.2))
    {h : A â†’ B âŠ• C} (hh : IsHom h)
    : IsHom (fun x â†¦ Sum.elim (f x) (g x) (h x)) := by
  have {x}
      : Sum.elim (f x) (g x) (h x)
      = Sum.elim (Î³ := A â†’ğ’’ D) (fun x â†¦ .mk (f Â· x)) (fun x â†¦ .mk (g Â· x)) (h x) x := by
    cases h x <;> rfl
  simp only [this]
  fun_prop

@[fun_prop]
lemma isHom_map
    {f : A â†’ B â†’ D} (hf : IsHom fun x : A Ã— B â†¦ f x.1 x.2)
    {g : A â†’ C â†’ E} (hg : IsHom fun x : A Ã— C â†¦ g x.1 x.2)
    {h : A â†’ B âŠ• C} (hh : IsHom h)
    : IsHom (fun x â†¦ Sum.map (f x) (g x) (h x)) := by
  change IsHom fun x â†¦ Sum.elim (Sum.inl âˆ˜ f x) (Sum.inr âˆ˜ g x) (h x)
  fun_prop

@[fun_prop, simp]
lemma isHom_isLeft : IsHom (Sum.isLeft : A âŠ• B â†’ Bool) := by
  have : (Sum.isLeft : A âŠ• B â†’ Bool) = Sum.elim (fun _ â†¦ true) (fun _ â†¦ false) := by
    ext x
    cases x <;> rfl
  rw [this]
  fun_prop

end QuasiBorelSpace.Sum
