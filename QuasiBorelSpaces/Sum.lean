import QuasiBorelSpaces.Bool
import QuasiBorelSpaces.Hom
import QuasiBorelSpaces.Lift
import QuasiBorelSpaces.OmegaCompletePartialOrder.Sum
import QuasiBorelSpaces.OmegaQuasiBorelSpace
import QuasiBorelSpaces.Prop
import QuasiBorelSpaces.Sigma

/-!
# Binary Coproducts of Quasi-Borel Spaces

This file defines binary coproducts of quasi-borel spaces by giving a
`QuasiBorelSpace` instance for the `¬∑ ‚äï ¬∑` type.

See [HeunenKSY17], Proposition 17.
-/

namespace QuasiBorelSpace.Sum

universe u v

variable
  {A : Type*} [QuasiBorelSpace A]
  {B : Type*} [QuasiBorelSpace B]
  {C : Type*} [QuasiBorelSpace C]
  {D : Type*} [QuasiBorelSpace D]
  {E : Type*} [QuasiBorelSpace E]

/--
We derive the `QuasiBorelSpace` instance for `A ‚äï B` via `Sigma (Encoding A B)`.
-/
def Encoding (A : Type u) (B : Type v) : Bool ‚Üí Type (max u v)
  | true => ULift A
  | false => ULift B

namespace Encoding

/-- The encoded version of `Sum.inl`. -/
def inl (x : A) : Sigma (Encoding A B) := ‚ü®true, ‚ü®x‚ü©‚ü©

/-- The encoded version of `Sum.inr`. -/
def inr (x : B) : Sigma (Encoding A B) := ‚ü®false, ‚ü®x‚ü©‚ü©

/-- The encoded version of `Sum.elim`. -/
def elim (f : A ‚Üí C) (g : B ‚Üí C) : Sigma (Encoding A B) ‚Üí C
  | ‚ü®true, x‚ü© => f x.down
  | ‚ü®false, x‚ü© => g x.down

instance {b : Bool} : QuasiBorelSpace (Encoding A B b) := by
  cases b <;>
  ¬∑ dsimp only [Encoding]
    infer_instance

@[fun_prop]
lemma isHom_inl : IsHom (inl (A := A) (B := B)) := by
  unfold inl
  fun_prop

@[fun_prop]
lemma isHom_inr : IsHom (inr (A := A) (B := B)) := by
  unfold inr
  fun_prop

@[fun_prop]
lemma isHom_elim {f : A ‚Üí C} (hf : IsHom f) {g : B ‚Üí C} (hg : IsHom g) : IsHom (elim f g) := by
  apply Sigma.isHom_elim fun b ‚Ü¶ ?_
  cases b <;>
  ¬∑ simp only [elim]
    fun_prop

end Encoding

/-- Encodes an `A ‚äï B` as a `Sigma (Encoding A B)`. -/
def encode : A ‚äï B ‚Üí Sigma (Encoding A B) :=
  Sum.elim Encoding.inl Encoding.inr

instance : QuasiBorelSpace (A ‚äï B) := lift encode

@[fun_prop]
lemma isHom_encode : IsHom (encode (A := A) (B := B)) := by
  apply isHom_of_lift

@[simp]
lemma isHom_inl : IsHom (Sum.inl : A ‚Üí A ‚äï B) := by
  simp only [isHom_to_lift, encode, Sum.elim_inl]
  fun_prop

@[fun_prop]
lemma isHom_inl' {f : A ‚Üí B} (hf : IsHom f) : IsHom (fun x ‚Ü¶ Sum.inl (f x) : A ‚Üí B ‚äï C) :=
  isHom_comp isHom_inl hf

@[simp]
lemma isHom_inr : IsHom (Sum.inr : B ‚Üí A ‚äï B) := by
  simp only [isHom_to_lift, encode, Sum.elim_inr]
  fun_prop

@[fun_prop]
lemma isHom_inr' {f : A ‚Üí C} (hf : IsHom f) : IsHom (fun x ‚Ü¶ Sum.inr (f x) : A ‚Üí B ‚äï C) :=
  isHom_comp isHom_inr hf

@[local fun_prop]
lemma isHom_elim
    {f : A ‚Üí C} (hf : IsHom f)
    {g : B ‚Üí C} (hg : IsHom g)
    : IsHom (Sum.elim f g) := by
  have : Sum.elim f g = fun x ‚Ü¶ Encoding.elim f g (encode x) := by
    ext x
    cases x <;> rfl
  rw [this]
  fun_prop

@[fun_prop]
lemma isHom_elim'
    {f : A ‚Üí B ‚Üí D} (hf : IsHom (fun x : A √ó B ‚Ü¶ f x.1 x.2))
    {g : A ‚Üí C ‚Üí D} (hg : IsHom (fun x : A √ó C ‚Ü¶ g x.1 x.2))
    {h : A ‚Üí B ‚äï C} (hh : IsHom h)
    : IsHom (fun x ‚Ü¶ Sum.elim (f x) (g x) (h x)) := by
  have {x}
      : Sum.elim (f x) (g x) (h x)
      = Sum.elim (Œ≥ := A ‚Üíùíí D) (fun x ‚Ü¶ .mk (f ¬∑ x)) (fun x ‚Ü¶ .mk (g ¬∑ x)) (h x) x := by
    cases h x <;> rfl
  simp only [this]
  fun_prop

@[fun_prop]
lemma isHom_map
    {f : A ‚Üí B ‚Üí D} (hf : IsHom fun x : A √ó B ‚Ü¶ f x.1 x.2)
    {g : A ‚Üí C ‚Üí E} (hg : IsHom fun x : A √ó C ‚Ü¶ g x.1 x.2)
    {h : A ‚Üí B ‚äï C} (hh : IsHom h)
    : IsHom (fun x ‚Ü¶ Sum.map (f x) (g x) (h x)) := by
  change IsHom fun x ‚Ü¶ Sum.elim (Sum.inl ‚àò f x) (Sum.inr ‚àò g x) (h x)
  fun_prop

@[fun_prop, simp]
lemma isHom_isLeft : IsHom (Sum.isLeft : A ‚äï B ‚Üí Bool) := by
  have : (Sum.isLeft : A ‚äï B ‚Üí Bool) = Sum.elim (fun _ ‚Ü¶ true) (fun _ ‚Ü¶ false) := by
    ext x
    cases x <;> rfl
  rw [this]
  fun_prop

end QuasiBorelSpace.Sum

namespace OmegaQuasiBorelSpace.Sum

open QuasiBorelSpace
open OmegaCompletePartialOrder

variable {A B : Type*}

/-- Coproduct of omega quasi-borel spaces is again an omega quasi-borel space. -/
noncomputable instance instOmegaQuasiBorelSpaceSum
    [OmegaQuasiBorelSpace A] [OmegaQuasiBorelSpace B] :
    OmegaQuasiBorelSpace (A ‚äï B) where
  isHom_œâSup' c hc := by
    simp only [œâSup]

    wlog hŒ± : Nonempty A
    ¬∑ have : ‚àÄn r, ‚àÉx, c n r = .inr x := by
        intro n r
        cases c n r with
        | inl x =>
          have : Nonempty A := ‚ü®x‚ü©
          contradiction
        | inr x => simp only [Sum.inr.injEq, exists_eq']
      choose x hx using this

      have hx' {a} : Monotone (fun n ‚Ü¶ x n a) := by
        intro n‚ÇÅ n‚ÇÇ hn
        simp only
        suffices Sum.inr (x n‚ÇÅ a) ‚â§ (Sum.inr (x n‚ÇÇ a) : A ‚äï B) by
          simpa only [ge_iff_le, Sum.inr_le_inr_iff] using this
        simp only [‚Üê hx]
        apply c.monotone hn

      have hx'' : Monotone x := by
        intro n‚ÇÅ n‚ÇÇ hn r
        simp only
        suffices Sum.inr (x n‚ÇÅ r) ‚â§ (Sum.inr (x n‚ÇÇ r) : A ‚äï B) by
          simpa only [ge_iff_le, Sum.inr_le_inr_iff] using this
        simp only [‚Üê hx]
        apply c.monotone hn

      have (a : ‚Ñù) : c.map (Pi.evalOrderHom a) = Chain.Sum.inr ‚ü®fun n ‚Ü¶ x n a, hx'‚ü© := by
        ext n
        simp only [
          Chain.map_coe, Pi.evalOrderHom_coe,
          Function.comp_apply, Function.eval, hx,
          Chain.Sum.inr_apply, Sum.inr.injEq]
        rfl

      simp only [this, Chain.Sum.distrib_inr, Sum.map_inr]
      apply Sum.isHom_inr'
      change IsHom (œâSup ‚ü®_, hx''‚ü©)
      apply isHom_œâSup
      intro n
      change IsHom (x n)

      have hŒ±' : IsEmpty A := by simpa only [not_nonempty_iff] using hŒ±
      have : IsHom (fun r ‚Ü¶ Sum.elim hŒ±'.elim id (c n r)) := by
        apply Sum.isHom_elim'
        ¬∑ rw [isHom_def]
          intro œÜ
          have : Nonempty A := ‚ü®(œÜ 0).2‚ü©
          contradiction
        ¬∑ fun_prop
        ¬∑ fun_prop

      simp only [hx, Sum.elim_inr, id_eq] at this
      apply this

    wlog hŒ≤ : Nonempty B
    ¬∑ have : ‚àÄn r, ‚àÉx, c n r = .inl x := by
        intro n r
        cases c n r with
        | inr x =>
          have : Nonempty B := ‚ü®x‚ü©
          contradiction
        | inl x => simp only [Sum.inl.injEq, exists_eq']
      choose x hx using this

      have hx' {a} : Monotone (fun n ‚Ü¶ x n a) := by
        intro n‚ÇÅ n‚ÇÇ hn
        simp only
        suffices Sum.inl (x n‚ÇÅ a) ‚â§ (Sum.inl (x n‚ÇÇ a) : A ‚äï B) by
          simpa only [ge_iff_le, Sum.inl_le_inl_iff] using this
        simp only [‚Üê hx]
        apply c.monotone hn

      have hx'' : Monotone x := by
        intro n‚ÇÅ n‚ÇÇ hn r
        simp only
        suffices Sum.inl (x n‚ÇÅ r) ‚â§ (Sum.inl (x n‚ÇÇ r) : A ‚äï B) by
          simpa only [ge_iff_le, Sum.inl_le_inl_iff] using this
        simp only [‚Üê hx]
        apply c.monotone hn

      have (a : ‚Ñù) : c.map (Pi.evalOrderHom a) = Chain.Sum.inl ‚ü®fun n ‚Ü¶ x n a, hx'‚ü© := by
        ext n
        simp only [Chain.map_coe, Pi.evalOrderHom_coe, Function.comp_apply, Function.eval, hx]
        rfl

      simp only [this, Chain.Sum.distrib_inl, Sum.map_inl]
      apply Sum.isHom_inl'
      change IsHom (œâSup ‚ü®_, hx''‚ü©)
      apply isHom_œâSup
      intro n
      change IsHom (x n)

      have hŒ≤' : IsEmpty B := by simpa only [not_nonempty_iff] using hŒ≤
      have : IsHom (fun r ‚Ü¶ Sum.elim id hŒ≤'.elim (c n r)) := by
        apply Sum.isHom_elim'
        ¬∑ fun_prop
        ¬∑ rw [isHom_def]
          intro œÜ
          have : Nonempty B := ‚ü®(œÜ 0).2‚ü©
          contradiction
        ¬∑ fun_prop

      simp only [hx, Sum.elim_inl, id_eq] at this
      apply this

    have : Inhabited A := ‚ü®hŒ±.some‚ü©
    have : Inhabited B := ‚ü®hŒ≤.some‚ü©
    simp only [
      Chain.Sum.distrib_def, Chain.map_coe,
      Pi.evalOrderHom_coe, Function.comp_apply, Function.eval]

    simp only [apply_ite, Sum.map_inl, Sum.map_inr]
    apply Prop.isHom_ite
    ¬∑ apply isHom_cases (f := fun x _ ‚Ü¶ x = true)
      ¬∑ fun_prop
      ¬∑ fun_prop
    ¬∑ apply Sum.isHom_inl'
      change IsHom (œâSup ‚ü®_, ?_‚ü©)
      ¬∑ apply isHom_œâSup
        intro n
        apply Sum.isHom_elim'
        ¬∑ fun_prop
        ¬∑ fun_prop
        ¬∑ apply hc
      ¬∑ intro n‚ÇÅ n‚ÇÇ hn x
        have : c n‚ÇÅ x ‚â§ c n‚ÇÇ x := c.monotone hn x
        simp only [
          id_eq, Chain.map_coe, Pi.evalOrderHom_coe, Function.comp_apply,
          Function.eval, ge_iff_le]
        cases hcn‚ÇÅ : c n‚ÇÅ x with
        | inl cn‚ÇÅ =>
          cases hcn‚ÇÇ : c n‚ÇÇ x with
          | inl hcn‚ÇÇ => simpa only [hcn‚ÇÅ, hcn‚ÇÇ, Sum.inl_le_inl_iff] using this
          | inr hcn‚ÇÇ => simp only [hcn‚ÇÅ, hcn‚ÇÇ, Sum.not_inl_le_inr] at this
        | inr cn‚ÇÅ =>
          cases hcn‚ÇÇ : c n‚ÇÇ x with
          | inl hcn‚ÇÇ => simp only [hcn‚ÇÅ, hcn‚ÇÇ, Sum.not_inr_le_inl] at this
          | inr hcn‚ÇÇ => simp only [le_refl]
    ¬∑ apply Sum.isHom_inr'
      change IsHom (œâSup ‚ü®_, ?_‚ü©)
      ¬∑ apply isHom_œâSup
        intro n
        apply Sum.isHom_elim'
        ¬∑ fun_prop
        ¬∑ fun_prop
        ¬∑ apply isHom_comp'
          ¬∑ apply Sum.isHom_elim'
            ¬∑ fun_prop
            ¬∑ fun_prop
            ¬∑ fun_prop
          ¬∑ apply hc
      ¬∑ intro n‚ÇÅ n‚ÇÇ hn x
        have : c n‚ÇÅ x ‚â§ c n‚ÇÇ x := c.monotone hn x
        simp only [
          id_eq, Chain.map_coe, Pi.evalOrderHom_coe, Function.comp_apply,
          Function.eval, ge_iff_le]
        cases hcn‚ÇÅ : c n‚ÇÅ x with
        | inl cn‚ÇÅ =>
          cases hcn‚ÇÇ : c n‚ÇÇ x with
          | inl hcn‚ÇÇ => simp only [OrderHom.coe_mk, Sum.swap_inl, le_refl]
          | inr hcn‚ÇÇ => simp only [hcn‚ÇÅ, hcn‚ÇÇ, Sum.not_inl_le_inr] at this
        | inr cn‚ÇÅ =>
          cases hcn‚ÇÇ : c n‚ÇÇ x with
          | inl hcn‚ÇÇ => simp only [hcn‚ÇÅ, hcn‚ÇÇ, Sum.not_inr_le_inl] at this
          | inr hcn‚ÇÇ =>
            simpa only [OrderHom.coe_mk, Sum.swap_inr, hcn‚ÇÅ, hcn‚ÇÇ, Sum.inr_le_inr_iff] using this

end OmegaQuasiBorelSpace.Sum
