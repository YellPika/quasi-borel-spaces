import QuasiBorelSpaces.PreProbabilityMeasure
import QuasiBorelSpaces.ENNReal

open MeasureTheory
open scoped unitInterval

namespace QuasiBorelSpace

variable {A B C A' B' C' : Type*}
  [QuasiBorelSpace A] [QuasiBorelSpace B] [QuasiBorelSpace C]
  [QuasiBorelSpace A'] [QuasiBorelSpace B'] [QuasiBorelSpace C']

/-- The type of _(quasi-borel) probability measures_. -/
structure ProbabilityMeasure (A : Type*) [QuasiBorelSpace A] where
  private fromQuotient ::
  private val : Quotient (PreProbabilityMeasure.setoid A)

namespace ProbabilityMeasure

/-- Constructs a `ProbabilityMeasure` from a `PreProbabilityMeasure`. -/
def mk (Œº : PreProbabilityMeasure A) : ProbabilityMeasure A := ‚ü®‚ü¶Œº‚üß‚ü©

@[simp]
lemma mk_eq_iff (Œº ŒΩ : PreProbabilityMeasure A) : mk Œº = mk ŒΩ ‚Üî Œº ‚âà ŒΩ := by
  simp only [mk, fromQuotient.injEq, Quotient.eq, PreProbabilityMeasure.setoid_r]

@[induction_eliminator, cases_eliminator]
lemma inductionOn
    {motive : ProbabilityMeasure A ‚Üí Prop} (Œº : ProbabilityMeasure A)
    (mk : (Œº : PreProbabilityMeasure A) ‚Üí motive (mk Œº))
    : motive Œº := by
  rcases Œº with ‚ü®Œº‚ü©
  induction Œº using Quotient.inductionOn with | h Œº =>
  rcases Œº with ‚ü®Œ±, hŒ±, Œº‚ü©
  apply mk

/-- The integral of a function over a `ProbabilityMeasure`. -/
noncomputable def lintegral (k : A ‚Üí ENNReal) (Œº : ProbabilityMeasure A) : ENNReal :=
  open Classical in
  if hk : IsHom k then
    Œº.val.liftOn' (PreProbabilityMeasure.lintegral k) (fun _ _ h ‚Ü¶ by
      apply h
      apply hk)
  else
    0

open Classical in
@[simp]
lemma lintegral_mk
    (k : A ‚Üí ENNReal) (Œº : PreProbabilityMeasure A)
    : lintegral k (mk Œº)
    = if IsHom k then Œº.lintegral k else 0 := by
  rfl

@[ext]
lemma ext
    {Œº‚ÇÅ Œº‚ÇÇ : ProbabilityMeasure A}
    (hŒº : ‚àÄ {k}, IsHom k ‚Üí Œº‚ÇÅ.lintegral k = Œº‚ÇÇ.lintegral k)
    : Œº‚ÇÅ = Œº‚ÇÇ := by
  cases Œº‚ÇÅ with | mk Œº =>
  cases Œº‚ÇÇ with | mk ŒΩ =>
  simp only [mk_eq_iff, PreProbabilityMeasure.equiv_def]
  intro k hk
  specialize hŒº hk
  simp only [lintegral_mk, hk, ‚ÜìreduceIte] at hŒº
  exact hŒº

/--
Converts a `ProbabilityMeasure` to the underlying `PreProbabilityMeasure`. This
may or may not be the one passed to `mk`, but will always be equivalent
(`(mk Œº).toPreDist ‚âà Œº`).
-/
noncomputable def toPreDist (Œº : ProbabilityMeasure A) : PreProbabilityMeasure A := Œº.val.out

instance : QuasiBorelSpace (ProbabilityMeasure A) := lift toPreDist

lemma toPreDist_mk (Œº : PreProbabilityMeasure A) : toPreDist (mk Œº) ‚âà Œº := by
  apply Quotient.exact
  simp only [toPreDist, mk, Quotient.out_eq]

@[simp, fun_prop]
lemma isHom_toPreDist : IsHom (toPreDist (A := A)) := by
  apply isHom_of_lift

@[simp, fun_prop]
lemma isHom_mk : IsHom (mk (A := A)) := by
  rw [isHom_to_lift, PreProbabilityMeasure.isHom_congr toPreDist_mk]
  fun_prop

@[simp]
lemma lintegral_toPredist
    (Œº : ProbabilityMeasure A) {k : A ‚Üí ENNReal} (hk : IsHom k)
    : Œº.toPreDist.lintegral k = Œº.lintegral k := by
  cases Œº with | mk Œº =>
  simp only [lintegral_mk, hk, ‚ÜìreduceIte]
  apply toPreDist_mk Œº hk

@[fun_prop]
lemma isHom_lintegral
    {k : A ‚Üí B ‚Üí ENNReal} (hk : IsHom fun (x, y) ‚Ü¶ k x y)
    {f : A ‚Üí ProbabilityMeasure B} (hf : IsHom f)
    : IsHom (fun x ‚Ü¶ lintegral (k x) (f x)) := by
  have hk' {x} : IsHom (k x) := by fun_prop
  have {x} : lintegral (k x) (f x) = PreProbabilityMeasure.lintegral (k x) (f x).toPreDist := by
    simp only [hk', lintegral_toPredist]
  simp only [this]
  fun_prop

lemma lintegral_add
    {f : A ‚Üí ENNReal} (hf : IsHom f)
    {g : A ‚Üí ENNReal} (hg : IsHom g)
    (Œº : ProbabilityMeasure A)
    : lintegral (f + g) Œº = lintegral f Œº + lintegral g Œº := by
  cases Œº with | mk Œº =>
  simp only [lintegral_mk]
  have : IsHom (f + g) := by
    change IsHom (fun x ‚Ü¶ f x + g x)
    fun_prop
  simp only [hf, ‚ÜìreduceIte, hg, this]
  apply PreProbabilityMeasure.lintegral_add_left
  exact hf

lemma lintegral_add'
    {f : A ‚Üí ENNReal} (hf : IsHom f)
    {g : A ‚Üí ENNReal} (hg : IsHom g)
    (Œº : ProbabilityMeasure A)
    : lintegral (fun x ‚Ü¶ f x + g x) Œº = lintegral f Œº + lintegral g Œº := by
  apply lintegral_add hf hg

lemma lintegral_mul_left
    (c : ENNReal) {f : A ‚Üí ENNReal} (hf : IsHom f) (Œº : ProbabilityMeasure A)
    : lintegral (fun x ‚Ü¶ c * f x) Œº = c * lintegral f Œº := by
  cases Œº with | mk Œº =>
  have : IsHom fun x ‚Ü¶ c * f x := by fun_prop
  simp only [lintegral_mk, this, ‚ÜìreduceIte, hf]
  simp  (disch := fun_prop) only [PreProbabilityMeasure.lintegral_mul_left]

lemma lintegral_mul_right
    (c : ENNReal) {f : A ‚Üí ENNReal} (hf : IsHom f) (Œº : ProbabilityMeasure A)
    : lintegral (fun x ‚Ü¶ f x * c) Œº = lintegral f Œº * c := by
  cases Œº with | mk Œº =>
  have : IsHom fun x ‚Ü¶ f x * c := by fun_prop
  simp only [lintegral_mk, this, ‚ÜìreduceIte, hf]
  simp (disch := fun_prop) only [PreProbabilityMeasure.lintegral_mul_right]

/-- The monadic `unit` operation. -/
noncomputable def unit (x : A) : ProbabilityMeasure A := mk (PreProbabilityMeasure.unit x)

@[simp, fun_prop]
lemma isHom_unit : IsHom (unit (A := A)) := by
  unfold unit
  fun_prop

@[simp]
lemma lintegral_unit {k : A ‚Üí ENNReal} (hk : IsHom k) (x : A) : lintegral k (unit x) = k x := by
  simp only [unit, lintegral_mk, hk, ‚ÜìreduceIte, PreProbabilityMeasure.lintegral_unit]

/-- The monadic `bind` operation. -/
noncomputable def bind
    (f : A ‚Üí ProbabilityMeasure B) (Œº : ProbabilityMeasure A)
    : ProbabilityMeasure B :=
  mk (PreProbabilityMeasure.bind (fun x ‚Ü¶ (f x).toPreDist) Œº.toPreDist)

@[simp, fun_prop]
lemma isHom_bind {f : A ‚Üí ProbabilityMeasure B} (hf : IsHom f) : IsHom (bind f) := by
  unfold bind
  fun_prop

@[simp]
lemma lintegral_bind
    {k : B ‚Üí ENNReal} (hk : IsHom k)
    {f : A ‚Üí ProbabilityMeasure B} (hf : IsHom f)
    (Œº : ProbabilityMeasure A)
    : lintegral k (bind f Œº) = lintegral (fun x ‚Ü¶ lintegral k (f x)) Œº := by
  cases Œº with | mk Œº =>
  have : IsHom fun x ‚Ü¶ lintegral k (f x) := by fun_prop
  simp only [bind, lintegral_mk, hk, ‚ÜìreduceIte, this]
  rw [PreProbabilityMeasure.lintegral_bind]
  ¬∑ trans Œº.lintegral (fun x ‚Ü¶ (f x).toPreDist.lintegral k)
    ¬∑ apply toPreDist_mk
      fun_prop
    ¬∑ congr
      ext x
      cases f x with | mk ŒΩ =>
      simp only [hk, lintegral_toPredist, lintegral_mk, ‚ÜìreduceIte]
  ¬∑ fun_prop
  ¬∑ fun_prop

@[simp]
lemma bind_unit {f : A ‚Üí ProbabilityMeasure B} (hf : IsHom f) (x : A) : bind f (unit x) = f x := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_bind, lintegral_unit]

@[simp]
lemma unit_bind (Œº : ProbabilityMeasure A) : bind unit Œº = Œº := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_bind, lintegral_unit]

@[simp]
lemma bind_bind
    {f : B ‚Üí ProbabilityMeasure C} (hf : IsHom f)
    {g : A ‚Üí ProbabilityMeasure B} (hg : IsHom g)
    (Œº : ProbabilityMeasure A)
    : bind f (bind g Œº) = bind (fun x ‚Ü¶ bind f (g x)) Œº := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_bind]

/-- The functorial `map` operation. -/
noncomputable def map (f : A ‚Üí B) (Œº : ProbabilityMeasure A) : ProbabilityMeasure B :=
  bind (fun x ‚Ü¶ unit (f x)) Œº

@[fun_prop]
lemma isHom_map {f : A ‚Üí B} (hf : IsHom f) : IsHom (map f) := by
  unfold map
  fun_prop

@[simp]
lemma lintegral_map
    {k : B ‚Üí ENNReal} (hk : IsHom k)
    {f : A ‚Üí B} (hf : IsHom f) (Œº : ProbabilityMeasure A)
    : lintegral k (map f Œº) = lintegral (fun x ‚Ü¶ k (f x)) Œº := by
  simp (disch := fun_prop) only [map, lintegral_bind, lintegral_unit]

@[simp]
lemma map_id : map (fun x : A ‚Ü¶ x) = id := by
  funext Œº
  simp only [map, unit_bind, id_eq]

@[simp]
lemma map_id' : map (A := A) id = id := map_id

@[simp]
lemma map_map
    {f : B ‚Üí C} (hf : IsHom f)
    {g : A ‚Üí B} (hg : IsHom g)
    (Œº : ProbabilityMeasure A)
    : map f (map g Œº) = map (fun x ‚Ü¶ f (g x)) Œº := by
  simp (disch := fun_prop) only [map, bind_bind, bind_unit]

@[simp]
lemma map_bind
    {f : B ‚Üí C} (hf : IsHom f)
    {g : A ‚Üí ProbabilityMeasure B} (hg : IsHom g)
    (Œº : ProbabilityMeasure A)
    : map f (bind g Œº) = bind (fun x ‚Ü¶ map f (g x)) Œº := by
  simp (disch := fun_prop) only [map, bind_bind]

@[simp]
lemma bind_map
    {f : B ‚Üí ProbabilityMeasure C} (hf : IsHom f)
    {g : A ‚Üí B} (hg : IsHom g)
    (Œº : ProbabilityMeasure A)
    : bind f (map g Œº) = bind (fun x ‚Ü¶ f (g x)) Œº := by
  simp (disch := fun_prop) only [map, bind_bind, bind_unit]

@[simp]
lemma map_unit {f : A ‚Üí B} (hf : IsHom f) (x : A) : map f (unit x) = unit (f x) := by
  simp (disch := fun_prop) only [map, bind_unit]

@[simp]
lemma bind_unit_eq_map {f : A ‚Üí B} : bind (fun x ‚Ü¶ unit (f x)) = map f := by
  funext Œº
  simp only [map]

/-- The functorial `str`ength operation. -/
noncomputable def str (x : A) (Œº : ProbabilityMeasure B) : ProbabilityMeasure (A √ó B) :=
  mk (PreProbabilityMeasure.str x Œº.toPreDist)

@[simp]
lemma lintegral_str
    {k : A √ó B ‚Üí ENNReal} (hk : IsHom k)
    (x : A) (Œº : ProbabilityMeasure B)
    : lintegral k (str x Œº) = lintegral (fun y ‚Ü¶ k (x, y)) Œº := by
  cases Œº with | mk Œº =>
  have : IsHom fun y ‚Ü¶ k (x, y) := by fun_prop
  simp only [
    str, lintegral_mk, hk, ‚ÜìreduceIte, PreProbabilityMeasure.lintegral_str,
    this, lintegral_toPredist]

@[simp, local fun_prop]
lemma isHom_str : IsHom (Function.uncurry (str (A := A) (B := B))) := by
  unfold Function.uncurry
  simp only [str]
  fun_prop

@[fun_prop]
lemma isHom_str'
    {f : A ‚Üí B} (hf : IsHom f)
    {g : A ‚Üí ProbabilityMeasure C} (hg : IsHom g)
    : IsHom (fun x ‚Ü¶ str (f x) (g x)) := by
  fun_prop

@[simp]
lemma str_eq_map (x : A) (Œº : ProbabilityMeasure B) : str x Œº = map (x, ¬∑) Œº := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_str, lintegral_map]

@[fun_prop]
lemma isHom_bind'
    {f : C ‚Üí A ‚Üí ProbabilityMeasure B} (hf : IsHom (Function.uncurry f))
    {g : C ‚Üí ProbabilityMeasure A} (hg : IsHom g)
    : IsHom (fun x ‚Ü¶ bind (f x) (g x)) := by
  have hf' : ‚àÄx, IsHom (f x) := by fun_prop
  have {x}
      : bind (f x) (g x)
      = bind (fun x : (A ‚Üíùíí ProbabilityMeasure B) √ó A ‚Ü¶ x.1 x.2) (str ‚ü®f x, hf' x‚ü© (g x)) := by
    simp only [
      str_eq_map, QuasiBorelHom.isHom_eval, Prod.isHom_iff, isHom_const,
      isHom_id', and_self, bind_map, QuasiBorelHom.coe_mk]
  simp only [this]
  fun_prop

@[fun_prop]
lemma isHom_map'
    {f : C ‚Üí A ‚Üí B} (hf : IsHom (Function.uncurry f))
    {g : C ‚Üí ProbabilityMeasure A} (hg : IsHom g)
    : IsHom (fun x ‚Ü¶ map (f x) (g x)) := by
  unfold map
  fun_prop

example (Œº : ProbabilityMeasure A) : str () Œº = map ((), ¬∑) Œº := by
  simp only [str_eq_map]

example (x : A) (y : B) : str x (unit y) = unit (x, y) := by
  simp only [str_eq_map, Prod.isHom_iff, isHom_const, isHom_id', and_self, map_unit]

example
    {f : A ‚Üí A'} (hf : IsHom f)
    {g : B ‚Üí B'} (hg : IsHom g)
    (x : A) (Œº : ProbabilityMeasure B)
    : map (Prod.map f g) (str x Œº) = str (f x) (map g Œº) := by
  simp (disch := fun_prop) only [str_eq_map, map_map, Prod.map_apply]

example
    (x : A) (Œº : ProbabilityMeasure (ProbabilityMeasure B))
    : bind (Function.uncurry str) (str x Œº) = str x (bind id Œº) := by
  simp only [
    str_eq_map, isHom_str, Prod.isHom_iff, isHom_const, isHom_id', and_self,
    bind_map, Function.uncurry_apply_pair, isHom_id, map_bind, id_eq]

/-- The Bernoulli measure. -/
noncomputable def coin (p : I) : ProbabilityMeasure Bool :=
  mk (.coin p)

@[simp]
lemma lintegral_coin
    (k : Bool ‚Üí ENNReal) (p : I)
    : lintegral k (coin p) = ENNReal.ofReal p * k true + ENNReal.ofReal (1 - p) * k false := by
  simp only [
    coin, lintegral_mk, isHom_of_discrete_countable,
    ‚ÜìreduceIte, PreProbabilityMeasure.lintegral_coin]

/-- Probabilistic choice. -/
noncomputable def choose (p : I) (Œº ŒΩ : ProbabilityMeasure A) : ProbabilityMeasure A :=
  bind (fun b ‚Ü¶ if b then Œº else ŒΩ) (coin p)

@[fun_prop]
lemma isHom_choose
    (p : I)
    {f : A ‚Üí ProbabilityMeasure B} (hf : IsHom f)
    {g : A ‚Üí ProbabilityMeasure B} (hg : IsHom g)
    : IsHom (fun x ‚Ü¶ choose p (f x) (g x)) := by
  simp only [choose]
  apply isHom_bind'
  ¬∑ unfold Function.uncurry
    dsimp only
    apply isHom_cases (ix := Prod.snd) (f := fun (b : Bool) x ‚Ü¶ if b then f x.1 else g x.1)
    ¬∑ fun_prop
    ¬∑ intro b
      cases b <;>
      ¬∑ simp only [Bool.false_eq_true, ‚ÜìreduceIte]
        fun_prop
  ¬∑ fun_prop

@[simp]
lemma lintegral_choose
    {k : A ‚Üí ENNReal} (hk : IsHom k)
    (p : I) (Œº ŒΩ : ProbabilityMeasure A)
    : lintegral k (choose p Œº ŒΩ)
    = ENNReal.ofReal p * lintegral k Œº + ENNReal.ofReal (œÉ p) * lintegral k ŒΩ := by
  simp (disch := fun_prop) only [choose, unitInterval.coe_symm_eq]
  rw [lintegral_bind, lintegral_coin]
  ¬∑ simp only [‚ÜìreduceIte, Bool.false_eq_true]
  ¬∑ fun_prop
  ¬∑ apply isHom_cases (f := fun (p : Bool) _ ‚Ü¶ if p then Œº else ŒΩ)
    ¬∑ fun_prop
    ¬∑ fun_prop

@[simp]
lemma choose_one (Œº ŒΩ : ProbabilityMeasure A) : choose 1 Œº ŒΩ = Œº := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_choose, Set.Icc.coe_one, ENNReal.ofReal_one, one_mul,
    unitInterval.symm_one, Set.Icc.coe_zero, ENNReal.ofReal_zero, zero_mul, add_zero]

@[simp]
lemma choose_zero (Œº ŒΩ : ProbabilityMeasure A) : choose 0 Œº ŒΩ = ŒΩ := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_choose, Set.Icc.coe_zero, ENNReal.ofReal_zero, zero_mul,
    unitInterval.symm_zero, Set.Icc.coe_one, ENNReal.ofReal_one, one_mul, zero_add]

@[simp]
lemma choose_eq (p : I) (Œº : ProbabilityMeasure A) : choose p Œº Œº = Œº := by
  rcases p with ‚ü®p, hp‚ü©
  simp only [Set.mem_Icc] at hp
  ext k hk
  simp (disch := fun_prop) [lintegral_choose, unitInterval.coe_symm_eq]
  simp only [hp, ENNReal.ofReal_sub, ENNReal.ofReal_one]

  wlog hkŒº : lintegral k Œº ‚â† ‚ä§
  ¬∑ simp only [ne_eq, Decidable.not_not] at hkŒº
    simp only [hkŒº, ENNReal.add_eq_top]
    by_cases h : p > 0
    ¬∑ simp only [ne_eq, ENNReal.ofReal_eq_zero, not_le, h, ENNReal.mul_top, true_or]
    ¬∑ have : p = 0 := by grind
      subst this
      simp only [ENNReal.ofReal_zero, zero_mul, ENNReal.zero_ne_top, tsub_zero, one_mul, or_true]

  rw [ENNReal.sub_mul]
  ¬∑ simp only [one_mul]
    apply add_tsub_cancel_of_le
    apply mul_le_of_le_one_left'
    simp only [ENNReal.ofReal_le_one, hp]
  ¬∑ simp only [ne_eq, hkŒº, not_false_eq_true, implies_true]

lemma choose_comm (p : I) (Œº ŒΩ : ProbabilityMeasure A) : choose p Œº ŒΩ = choose (œÉ p) ŒΩ Œº := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_choose, unitInterval.coe_symm_eq, unitInterval.symm_symm]
  rw [add_comm]

private lemma choose_assoc_bound {p q : I}
    (hp‚ÇÅ : 0 < p) (hp‚ÇÇ : p < 1)
    (hq‚ÇÅ : 0 < q) (hq‚ÇÇ : q < 1)
    : (œÉ p * q : ‚Ñù) / œÉ (p * q) ‚àà I := by
  have ‚ü®hq‚ÇÉ, hq‚ÇÑ‚ü© := q.property
  have ‚ü®hp‚ÇÉ, hp‚ÇÑ‚ü© := p.property
  simp only [unitInterval.coe_symm_eq, Set.mem_Icc] at ‚ä¢ hp‚ÇÉ hp‚ÇÑ hq‚ÇÉ hq‚ÇÑ
  apply And.intro
  ¬∑ apply div_nonneg
    ¬∑ rw [mul_nonneg_iff_left_nonneg_of_pos]
      ¬∑ simp only [sub_nonneg]
        assumption
      ¬∑ assumption
    ¬∑ grind
  ¬∑ rw [div_le_iff‚ÇÄ]
    ¬∑ simp only [Set.Icc.coe_mul, one_mul]
      rw [sub_mul]
      apply sub_le_sub
      ¬∑ simp only [one_mul]
        assumption
      ¬∑ rfl
    ¬∑ simp only [Set.Icc.coe_mul, sub_pos]
      sorry

lemma choose_assoc {p q} {Œº‚ÇÅ Œº‚ÇÇ Œº‚ÇÉ : ProbabilityMeasure A}
    (hp‚ÇÅ : 0 < p) (hp‚ÇÇ : p < 1)
    (hq‚ÇÅ : 0 < q) (hq‚ÇÇ : q < 1)
    : choose q (choose p Œº‚ÇÅ Œº‚ÇÇ) Œº‚ÇÉ
    = choose (p * q) Œº‚ÇÅ (choose ‚ü®_, choose_assoc_bound hp‚ÇÅ hp‚ÇÇ hq‚ÇÅ hq‚ÇÇ‚ü© Œº‚ÇÇ Œº‚ÇÉ) := by
  sorry

@[simp]
lemma bind_choose
    {f : A ‚Üí ProbabilityMeasure B} (hf : IsHom f)
    (p : unitInterval) (Œº ŒΩ : ProbabilityMeasure A)
    : bind f (choose p Œº ŒΩ) = choose p (bind f Œº) (bind f ŒΩ) := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_bind, lintegral_choose, unitInterval.coe_symm_eq]

@[simp]
lemma map_choose
    {f : A ‚Üí B} (hf : IsHom f)
    (p : unitInterval) (Œº ŒΩ : ProbabilityMeasure A)
    : map f (choose p Œº ŒΩ) = choose p (map f Œº) (map f ŒΩ) := by
  apply bind_choose
  fun_prop

@[simp]
lemma choose_bind
    {f : A ‚Üí ProbabilityMeasure B} (hf : IsHom f)
    {g : A ‚Üí ProbabilityMeasure B} (hg : IsHom g)
    (p : unitInterval) (Œº : ProbabilityMeasure A)
    : bind (fun x ‚Ü¶ choose p (f x) (g x)) Œº = choose p (bind f Œº) (bind g Œº) := by
  ext k hk
  simp (disch := fun_prop) only [
    lintegral_bind, lintegral_choose, unitInterval.coe_symm_eq,
    lintegral_add', lintegral_mul_left]

end QuasiBorelSpace.ProbabilityMeasure
