import QuasiBorelSpaces.ENNReal
import QuasiBorelSpaces.PreProbabilityMeasure
import QuasiBorelSpaces.SeparatesPoints

/-!
# Probability Measures over Quasi-Borel Spaces

This file defines probability measures over quasi-borel spaces.

See [HeunenKSY17], Section V-D.
-/

open MeasureTheory
open scoped unitInterval

namespace QuasiBorelSpace

variable {A B C A' B' C' : Type*}
  [QuasiBorelSpace A] [QuasiBorelSpace B] [QuasiBorelSpace C]
  [QuasiBorelSpace A'] [QuasiBorelSpace B'] [QuasiBorelSpace C']

/-! ## Basic Definitions -/

/-- The type of _(quasi-borel) probability measures_. -/
structure ProbabilityMeasure (A : Type*) [QuasiBorelSpace A] where
  private fromQuotient ::
  private val : Quotient (PreProbabilityMeasure.setoid A)

namespace ProbabilityMeasure

/-- Constructs a `ProbabilityMeasure` from a `PreProbabilityMeasure`. -/
def mk (Î¼ : PreProbabilityMeasure A) : ProbabilityMeasure A := âŸ¨âŸ¦Î¼âŸ§âŸ©

/--
Two `ProbabilityMeasure`s are equal iff their underlying
`PreProbabilityMeasure`s are equivalent.
-/
@[simp]
lemma mk_eq_iff (Î¼ Î½ : PreProbabilityMeasure A) : mk Î¼ = mk Î½ â†” Î¼ â‰ˆ Î½ := by
  simp only [mk, fromQuotient.injEq, Quotient.eq, PreProbabilityMeasure.setoid_r]

/-- Induction principle for `ProbabilityMeasure`. -/
@[induction_eliminator, cases_eliminator]
lemma inductionOn
    {motive : ProbabilityMeasure A â†’ Prop} (Î¼ : ProbabilityMeasure A)
    (mk : (Î¼ : PreProbabilityMeasure A) â†’ motive (mk Î¼))
    : motive Î¼ := by
  rcases Î¼ with âŸ¨Î¼âŸ©
  induction Î¼ using Quotient.inductionOn with | h Î¼ =>
  rcases Î¼ with âŸ¨Î±, hÎ±, Î¼âŸ©
  apply mk

/--
Converts a `ProbabilityMeasure` to the underlying `PreProbabilityMeasure`. This
may or may not be the one passed to `mk`, but will always be equivalent
(`(mk Î¼).toPreProbabilityMeasure â‰ˆ Î¼`).
-/
noncomputable def toPreProbabilityMeasure (Î¼ : ProbabilityMeasure A)
    : PreProbabilityMeasure A :=
  Î¼.val.out

lemma toPreProbabilityMeasure_mk (Î¼ : PreProbabilityMeasure A)
    : toPreProbabilityMeasure (mk Î¼) â‰ˆ Î¼ := by
  apply Quotient.exact
  simp only [toPreProbabilityMeasure, mk, Quotient.out_eq]

/-- Every `ProbabilityMeasure` has a nonempty carrier. -/
lemma nonempty (Î¼ : ProbabilityMeasure A) : Nonempty A := Î¼.toPreProbabilityMeasure.nonempty

/-! ## `QuasiBorelSpace` Instance -/

/-- The `QuasiBorelSpace` structure on `ProbabilityMeasure A`. -/
instance : QuasiBorelSpace (ProbabilityMeasure A) := lift toPreProbabilityMeasure

/-- `toPreProbabilityMeasure` is a homomorphism. -/
@[simp, fun_prop]
lemma isHom_toPreProbabilityMeasure : IsHom (toPreProbabilityMeasure (A := A)) := by
  apply isHom_of_lift

/-- `mk` is a homomorphism. -/
@[simp, fun_prop]
lemma isHom_mk : IsHom (mk (A := A)) := by
  rw [isHom_to_lift, PreProbabilityMeasure.isHom_congr toPreProbabilityMeasure_mk]
  fun_prop

/-! ## Integrals -/

/-- The integral of a function over a `ProbabilityMeasure`. -/
noncomputable def lintegral (k : A â†’ ENNReal) (Î¼ : ProbabilityMeasure A) : ENNReal :=
  Î¼.toPreProbabilityMeasure.lintegral k

@[inherit_doc lintegral]
scoped notation "âˆ«â» " x ", " m " âˆ‚" Î¼:70 => lintegral (fun x â†¦ m) Î¼

@[simp]
lemma lintegral_mk
    {k : A â†’ ENNReal} (hk : IsHom k) (Î¼ : PreProbabilityMeasure A)
    : âˆ«â» x, k x âˆ‚mk Î¼ = Î¼.lintegral k := by
  apply PreProbabilityMeasure.lintegral_congr hk
  apply toPreProbabilityMeasure_mk

/-- Converting to `PreProbabilityMeasure` and back preserves the integral. -/
@[simp]
lemma lintegral_toPreProbabilityMeasure
    (Î¼ : ProbabilityMeasure A) (k : A â†’ ENNReal)
    : Î¼.toPreProbabilityMeasure.lintegral k = âˆ«â» x, k x âˆ‚Î¼ := by
  rfl

/-- Two `ProbabilityMeasure`s are equal iff they have the same integrals. -/
@[ext]
lemma ext
    {Î¼â‚ Î¼â‚‚ : ProbabilityMeasure A}
    (hÎ¼ : âˆ€ {k}, IsHom k â†’ âˆ«â» x, k x âˆ‚Î¼â‚ = âˆ«â» x, k x âˆ‚Î¼â‚‚)
    : Î¼â‚ = Î¼â‚‚ := by
  cases Î¼â‚ with | mk Î¼ =>
  cases Î¼â‚‚ with | mk Î½ =>
  simp only [mk_eq_iff, PreProbabilityMeasure.equiv_def]
  intro k hk
  specialize hÎ¼ hk
  simp only [hk, lintegral_mk] at hÎ¼
  exact hÎ¼

/-- The integral of a homomorphism is itself a homomorphism. -/
@[fun_prop]
lemma isHom_lintegral
    {k : A â†’ B â†’ ENNReal} (hk : IsHom fun (x, y) â†¦ k x y)
    {f : A â†’ ProbabilityMeasure B} (hf : IsHom f)
    : IsHom (fun x â†¦ âˆ«â» y, k x y âˆ‚f x) := by
  simp (disch := fun_prop) only [â† lintegral_toPreProbabilityMeasure]
  fun_prop

/-- Linearity of integration: addition on the left. -/
lemma lintegral_add_left
    {f : A â†’ ENNReal} (hf : IsHom f)
    (g : A â†’ ENNReal)
    (Î¼ : ProbabilityMeasure A)
    : âˆ«â» x, f x + g x âˆ‚Î¼ = âˆ«â» x, f x âˆ‚Î¼ + âˆ«â» x, g x âˆ‚Î¼ := by
  cases Î¼ with | mk Î¼ =>
  simp (disch := fun_prop) only [â† lintegral_toPreProbabilityMeasure]
  apply PreProbabilityMeasure.lintegral_add_left hf g

/-- Linearity of integration: addition on the right. -/
lemma lintegral_add_right
    (f : A â†’ ENNReal)
    {g : A â†’ ENNReal} (hg : IsHom g)
    (Î¼ : ProbabilityMeasure A)
    : âˆ«â» x, f x + g x âˆ‚Î¼ = âˆ«â» x, f x âˆ‚Î¼ + âˆ«â» x, g x âˆ‚Î¼ := by
  cases Î¼ with | mk Î¼ =>
  simp (disch := fun_prop) only [â† lintegral_toPreProbabilityMeasure]
  apply PreProbabilityMeasure.lintegral_add_right f hg

/-- Linearity of integration: scalar multiplication on the left. -/
lemma lintegral_mul_left
    (c : ENNReal) {f : A â†’ ENNReal} (hf : IsHom f) (Î¼ : ProbabilityMeasure A)
    : âˆ«â» x, c * f x âˆ‚Î¼ = c * âˆ«â» x, f x âˆ‚Î¼ := by
  cases Î¼ with | mk Î¼ =>
  simp (disch := fun_prop) only [lintegral_mk, PreProbabilityMeasure.lintegral_mul_left]

/-- Linearity of integration: scalar multiplication on the right. -/
lemma lintegral_mul_right
    (c : ENNReal) {f : A â†’ ENNReal} (hf : IsHom f) (Î¼ : ProbabilityMeasure A)
    : âˆ«â» x, f x * c âˆ‚Î¼ = (âˆ«â» x, f x âˆ‚Î¼) * c := by
  cases Î¼ with | mk Î¼ =>
  simp (disch := fun_prop) only [lintegral_mk, PreProbabilityMeasure.lintegral_mul_right]

/-- The integral of a constant function is the constant. -/
@[simp]
lemma lintegral_const (c : ENNReal) (Î¼ : ProbabilityMeasure A) : âˆ«â» _, c âˆ‚Î¼ = c := by
  cases Î¼ with | mk Î¼ =>
  simp (disch := fun_prop) only [lintegral_mk, PreProbabilityMeasure.lintegral_const]

/-- Monotonicity of integration. -/
@[simp]
lemma lintegral_mono
    {f g : A â†’ ENNReal} (h : f â‰¤ g) (Î¼ : ProbabilityMeasure A)
    : âˆ«â» x, f x âˆ‚Î¼ â‰¤ âˆ«â» x, g x âˆ‚Î¼ := by
  unfold lintegral
  apply PreProbabilityMeasure.lintegral_mono h

/-- Monotone convergence theorem for integrals. -/
lemma lintegral_iSup
    (f : â„• â†’ A â†’ ENNReal) (hfâ‚ : Monotone f) (hfâ‚‚ : âˆ€ n, IsHom (f n)) (Î¼ : ProbabilityMeasure A)
    : â¨†n, âˆ«â» x, f n x âˆ‚Î¼ = âˆ«â» x, â¨†n, f n x âˆ‚Î¼ := by
  unfold lintegral
  have := PreProbabilityMeasure.lintegral_iSup f hfâ‚ hfâ‚‚ Î¼.toPreProbabilityMeasure
  simpa only [lintegral_toPreProbabilityMeasure, iSup_apply] using this

/-- The integral of a finite sum is the sum of the integrals. -/
lemma lintegral_finset_sum {A}
    (s : Finset A) {f : A â†’ B â†’ ENNReal}
    (hf : âˆ€ b âˆˆ s, IsHom (f b)) (Î¼ : ProbabilityMeasure B) :
    âˆ«â» a, âˆ‘ b âˆˆ s, f b a âˆ‚Î¼ = âˆ‘ b âˆˆ s, âˆ«â» a, f b a âˆ‚Î¼ := by
  unfold lintegral
  apply PreProbabilityMeasure.lintegral_finset_sum s hf

/-- Upper bound for subtraction of integrals. -/
lemma lintegral_sub_le
    (f : A â†’ ENNReal)
    {g : A â†’ ENNReal} (hg : IsHom g)
    (Î¼ : ProbabilityMeasure A)
    : âˆ«â» x, f x âˆ‚Î¼ - âˆ«â» x, g x âˆ‚Î¼ â‰¤ âˆ«â» x, f x - g x âˆ‚Î¼ := by
  unfold lintegral
  apply PreProbabilityMeasure.lintegral_sub_le f hg

/-! ## Measures -/

/-- The `FunLike` instance for `ProbabilityMeasure`. -/
noncomputable instance : FunLike (ProbabilityMeasure A) (Set A) ENNReal where
  coe Î¼ s := Î¼.toPreProbabilityMeasure.measureOf s
  coe_injective' := by
    intro Î¼â‚ Î¼â‚‚ h
    induction Î¼â‚ with | mk Î¼â‚ =>
    induction Î¼â‚‚ with | mk Î¼â‚‚ =>
    simp only [mk_eq_iff]
    rw [PreProbabilityMeasure.equiv_def']
    intro p hp
    grw [â†toPreProbabilityMeasure_mk Î¼â‚, â†toPreProbabilityMeasure_mk Î¼â‚‚]
    Â· apply congr_fun h
    Â· fun_prop
    Â· fun_prop

/-- The `OuterMeasureClass` instance for `ProbabilityMeasure`. -/
instance : OuterMeasureClass (ProbabilityMeasure A) A where
  measure_empty _ := by
    simp only [DFunLike.coe, PreProbabilityMeasure.measureOf_empty]
  measure_mono _ _ _ h := by
    apply PreProbabilityMeasure.measureOf_mono _ h
  measure_iUnion_nat_le _ _ _ := by
    apply PreProbabilityMeasure.measureOf_iUnion_le

/-! ## Point Separation -/

/-- The `SeparatesPoints` instance for `ProbabilityMeasure`. -/
instance : SeparatesPoints (ProbabilityMeasure A) where
  separates Î¼â‚ Î¼â‚‚ h := by
    ext k
    apply h _
    Â· apply isHom_comp'
      Â· rw [isHom_def]
        intro Ï† hÏ†
        simp only [isHom_ofMeasurableSpace] at âŠ¢ hÏ†
        have : MeasurableSet { x | x âˆˆ Ï† â»Â¹' ({âˆ«â» x, k x âˆ‚Î¼â‚} : Set _) } := by
          apply hÏ†
          apply measurableSet_eq
        simp only [Set.mem_preimage, Set.mem_singleton_iff, measurableSet_setOf] at this
        grind
      Â· fun_prop
    Â· rfl

/-! ## Operations -/

/-! ### `unit` -/

/-- The monadic `unit` operation. -/
noncomputable def unit (x : A) : ProbabilityMeasure A := mk (PreProbabilityMeasure.unit x)

@[simp, fun_prop]
lemma isHom_unit : IsHom (unit (A := A)) := by
  unfold unit
  fun_prop

@[simp]
lemma lintegral_unit {k : A â†’ ENNReal} (hk : IsHom k) (x : A) : âˆ«â» x, k x âˆ‚unit x = k x := by
  simp only [unit, hk, lintegral_mk, PreProbabilityMeasure.lintegral_unit]

/-- `unit` is injective when the carrier separates points. -/
@[simp]
lemma unit_injective [SeparatesPoints A] : Function.Injective (unit (A := A)) := by
  intro x y h
  simp only [ProbabilityMeasure.ext_iff] at h
  apply separatesPoints_def
  intro p hp hx
  classical
  have : IsHom fun x â†¦ if p x then 1 else (0 : ENNReal) := by
    apply Prop.isHom_ite <;> fun_prop
  specialize h this
  simp (disch := fun_prop) only [
    lintegral_unit, hx, â†“reduceIte, left_eq_ite_iff, one_ne_zero,
    imp_false, Decidable.not_not] at h
  exact h

/-- `unit` is injective iff the inputs are equal. -/
@[simp]
lemma unit_inj [SeparatesPoints A] (x y : A) : unit x = unit y â†” x = y := by
  apply Iff.intro
  Â· apply unit_injective
  Â· grind

/-- `A` separates points iff `unit` is injective. -/
lemma separatesPoints_iff_unit_injective
    : SeparatesPoints A â†” Function.Injective (unit (A := A)) := by
  apply Iff.intro
  Â· intro _
    apply unit_injective
  Â· intro h
    constructor
    intro x y h'
    apply h
    ext k hk
    apply h'
    Â· simp only [hk, lintegral_unit]
      apply isHom_comp' ?_ hk
      rw [isHom_def]
      intro Ï† hÏ†
      simp only [isHom_ofMeasurableSpace] at âŠ¢ hÏ†
      apply MeasurableSet.mem
      have : MeasurableSet fun r â†¦ r âˆˆ (Ï† â»Â¹' ({k x} : Set _)) := by
        apply hÏ†
        apply measurableSet_eq
      grind
    Â· rfl

/-! ### `bind` -/

/-- The monadic `bind` operation. -/
noncomputable def bind
    (f : A â†’ ProbabilityMeasure B) (Î¼ : ProbabilityMeasure A)
    : ProbabilityMeasure B :=
  mk (PreProbabilityMeasure.bind (fun x â†¦ (f x).toPreProbabilityMeasure) Î¼.toPreProbabilityMeasure)

@[simp, fun_prop]
lemma isHom_bind {f : A â†’ ProbabilityMeasure B} (hf : IsHom f) : IsHom (bind f) := by
  unfold bind
  fun_prop

/-- Computing the integral of `bind`. -/
@[simp]
lemma lintegral_bind
    {k : B â†’ ENNReal} (hk : IsHom k)
    {f : A â†’ ProbabilityMeasure B} (hf : IsHom f)
    (Î¼ : ProbabilityMeasure A)
    : âˆ«â» x, k x âˆ‚bind f Î¼ = âˆ«â» x, âˆ«â» y, k y âˆ‚f x âˆ‚Î¼ := by
  cases Î¼ with | mk Î¼ =>
  have : IsHom fun x â†¦ âˆ«â» x, k x âˆ‚f x := by fun_prop
  simp only [bind, hk, lintegral_mk, this]
  rw [PreProbabilityMeasure.lintegral_bind]
  Â· apply toPreProbabilityMeasure_mk
    fun_prop
  Â· fun_prop
  Â· fun_prop

/-- Left unit law for `bind`. -/
@[simp]
lemma bind_unit {f : A â†’ ProbabilityMeasure B} (hf : IsHom f) (x : A) : bind f (unit x) = f x := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_bind, lintegral_unit]

/-- Right unit law for `bind`. -/
@[simp]
lemma unit_bind (Î¼ : ProbabilityMeasure A) : bind unit Î¼ = Î¼ := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_bind, lintegral_unit]

/-- Associativity of `bind`. -/
@[simp]
lemma bind_bind
    {f : B â†’ ProbabilityMeasure C} (hf : IsHom f)
    {g : A â†’ ProbabilityMeasure B} (hg : IsHom g)
    (Î¼ : ProbabilityMeasure A)
    : bind f (bind g Î¼) = bind (fun x â†¦ bind f (g x)) Î¼ := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_bind]

/-! ### `map` -/

/-- The functorial `map` operation. -/
noncomputable def map (f : A â†’ B) (Î¼ : ProbabilityMeasure A) : ProbabilityMeasure B :=
  bind (fun x â†¦ unit (f x)) Î¼

@[fun_prop]
lemma isHom_map {f : A â†’ B} (hf : IsHom f) : IsHom (map f) := by
  unfold map
  fun_prop

/-- Computing the integral of `map`. -/
@[simp]
lemma lintegral_map
    {k : B â†’ ENNReal} (hk : IsHom k)
    {f : A â†’ B} (hf : IsHom f) (Î¼ : ProbabilityMeasure A)
    : âˆ«â» x, k x âˆ‚map f Î¼ = âˆ«â» x, k (f x) âˆ‚Î¼ := by
  simp (disch := fun_prop) only [map, lintegral_bind, lintegral_unit]

/-- `map` of the identity function is the identity. -/
@[simp]
lemma map_id : map (fun x : A â†¦ x) = id := by
  funext Î¼
  simp only [map, unit_bind, id_eq]

@[simp]
lemma map_id' : map (A := A) id = id := map_id

/-- Functor composition law for `map`. -/
@[simp]
lemma map_map
    {f : B â†’ C} (hf : IsHom f)
    {g : A â†’ B} (hg : IsHom g)
    (Î¼ : ProbabilityMeasure A)
    : map f (map g Î¼) = map (fun x â†¦ f (g x)) Î¼ := by
  simp (disch := fun_prop) only [map, bind_bind, bind_unit]

/-- Commutation of `map` and `bind`. -/
@[simp]
lemma map_bind
    {f : B â†’ C} (hf : IsHom f)
    {g : A â†’ ProbabilityMeasure B} (hg : IsHom g)
    (Î¼ : ProbabilityMeasure A)
    : map f (bind g Î¼) = bind (fun x â†¦ map f (g x)) Î¼ := by
  simp (disch := fun_prop) only [map, bind_bind]

/-- Commutation of `bind` and `map`. -/
@[simp]
lemma bind_map
    {f : B â†’ ProbabilityMeasure C} (hf : IsHom f)
    {g : A â†’ B} (hg : IsHom g)
    (Î¼ : ProbabilityMeasure A)
    : bind f (map g Î¼) = bind (fun x â†¦ f (g x)) Î¼ := by
  simp (disch := fun_prop) only [map, bind_bind, bind_unit]

/-- `map` commutes with `unit`. -/
@[simp]
lemma map_unit {f : A â†’ B} (hf : IsHom f) (x : A) : map f (unit x) = unit (f x) := by
  simp (disch := fun_prop) only [map, bind_unit]

/-- `bind` with `unit` is equivalent to `map`. -/
@[simp]
lemma bind_unit_eq_map {f : A â†’ B} : bind (fun x â†¦ unit (f x)) = map f := by
  funext Î¼
  simp only [map]

/-! ### `str` -/

/-- The functorial `str`ength operation. -/
noncomputable def str (x : A) (Î¼ : ProbabilityMeasure B) : ProbabilityMeasure (A Ã— B) :=
  mk (PreProbabilityMeasure.str x Î¼.toPreProbabilityMeasure)

@[simp]
lemma lintegral_str
    {k : A Ã— B â†’ ENNReal} (hk : IsHom k)
    (x : A) (Î¼ : ProbabilityMeasure B)
    : âˆ«â» p, k p âˆ‚str x Î¼ = âˆ«â» y, k (x, y) âˆ‚Î¼ := by
  cases Î¼ with | mk Î¼ =>
  simp (disch := fun_prop) only [
    str, lintegral_mk, PreProbabilityMeasure.lintegral_str,
    lintegral_toPreProbabilityMeasure]

/-- `str` is a homomorphism in both arguments. -/
@[simp, local fun_prop]
lemma isHom_str : IsHom (Function.uncurry (str (A := A) (B := B))) := by
  unfold Function.uncurry
  simp only [str]
  fun_prop

/-- `str` is a homomorphism when composed with other homomorphisms. -/
@[fun_prop]
lemma isHom_str'
    {f : A â†’ B} (hf : IsHom f)
    {g : A â†’ ProbabilityMeasure C} (hg : IsHom g)
    : IsHom (fun x â†¦ str (f x) (g x)) := by
  fun_prop

/-- `str` expressed in terms of `map`. -/
@[simp]
lemma str_eq_map (x : A) (Î¼ : ProbabilityMeasure B) : str x Î¼ = map (x, Â·) Î¼ := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_str, lintegral_map]

/-- Helper lemma for proving `bind` is a homomorphism with uncurried functions. -/
@[fun_prop]
lemma isHom_bind'
    {f : C â†’ A â†’ ProbabilityMeasure B} (hf : IsHom (Function.uncurry f))
    {g : C â†’ ProbabilityMeasure A} (hg : IsHom g)
    : IsHom (fun x â†¦ bind (f x) (g x)) := by
  have hf' : âˆ€x, IsHom (f x) := by fun_prop
  have {x}
      : bind (f x) (g x)
      = bind (fun x : (A â†’ğ’’ ProbabilityMeasure B) Ã— A â†¦ x.1 x.2) (str âŸ¨f x, hf' xâŸ© (g x)) := by
    simp only [
      str_eq_map, QuasiBorelHom.isHom_eval, Prod.isHom_iff, isHom_const,
      isHom_id', and_self, bind_map, QuasiBorelHom.coe_mk]
  simp only [this]
  fun_prop

/-- Helper lemma for proving `map` is a homomorphism with uncurried functions. -/
@[fun_prop]
lemma isHom_map'
    {f : C â†’ A â†’ B} (hf : IsHom (Function.uncurry f))
    {g : C â†’ ProbabilityMeasure A} (hg : IsHom g)
    : IsHom (fun x â†¦ map (f x) (g x)) := by
  unfold map
  fun_prop

example (Î¼ : ProbabilityMeasure A) : str () Î¼ = map ((), Â·) Î¼ := by
  simp only [str_eq_map]

example (x : A) (y : B) : str x (unit y) = unit (x, y) := by
  simp only [str_eq_map, Prod.isHom_iff, isHom_const, isHom_id', and_self, map_unit]

example
    {f : A â†’ A'} (hf : IsHom f)
    {g : B â†’ B'} (hg : IsHom g)
    (x : A) (Î¼ : ProbabilityMeasure B)
    : map (Prod.map f g) (str x Î¼) = str (f x) (map g Î¼) := by
  simp (disch := fun_prop) only [str_eq_map, map_map, Prod.map_apply]

example
    (x : A) (Î¼ : ProbabilityMeasure (ProbabilityMeasure B))
    : bind (Function.uncurry str) (str x Î¼) = str x (bind id Î¼) := by
  simp only [
    str_eq_map, isHom_str, Prod.isHom_iff, isHom_const, isHom_id', and_self,
    bind_map, Function.uncurry_apply_pair, isHom_id, map_bind, id_eq]

/-! ### `coin` -/

/-- The Bernoulli measure. -/
noncomputable def coin (p : I) : ProbabilityMeasure Bool :=
  mk (.coin p)

@[simp]
lemma lintegral_coin
    (k : Bool â†’ ENNReal) (p : I)
    : âˆ«â» x, k x âˆ‚coin p = ENNReal.ofReal p * k true + ENNReal.ofReal (1 - p) * k false := by
  simp only [coin, isHom_of_discrete_countable, lintegral_mk, PreProbabilityMeasure.lintegral_coin]

/-! ### `choose` -/

/-- Probabilistic choice. -/
noncomputable def choose (p : I) (Î¼ Î½ : ProbabilityMeasure A) : ProbabilityMeasure A :=
  bind (fun b â†¦ if b then Î¼ else Î½) (coin p)

@[inherit_doc choose]
scoped notation:65 Î¼ " â—ƒ" p "â–¹ " Î½:66 => choose p Î¼ Î½

/-- `choose` is a homomorphism. -/
@[fun_prop]
lemma isHom_choose
    (p : I)
    {f : A â†’ ProbabilityMeasure B} (hf : IsHom f)
    {g : A â†’ ProbabilityMeasure B} (hg : IsHom g)
    : IsHom (fun x â†¦ f x â—ƒpâ–¹ g x) := by
  simp only [choose]
  apply isHom_bind'
  Â· unfold Function.uncurry
    dsimp only
    apply isHom_cases (ix := Prod.snd) (f := fun (b : Bool) x â†¦ if b then f x.1 else g x.1)
    Â· fun_prop
    Â· intro b
      cases b <;>
      Â· simp only [Bool.false_eq_true, â†“reduceIte]
        fun_prop
  Â· fun_prop

@[simp]
lemma lintegral_choose
    {k : A â†’ ENNReal} (hk : IsHom k)
    (p : I) (Î¼ Î½ : ProbabilityMeasure A)
    : âˆ«â» x, k x âˆ‚(Î¼ â—ƒpâ–¹ Î½)
    = ENNReal.ofReal p * âˆ«â» x, k x âˆ‚Î¼ + ENNReal.ofReal (Ïƒ p) * âˆ«â» x, k x âˆ‚Î½ := by
  simp (disch := fun_prop) only [choose, unitInterval.coe_symm_eq]
  rw [lintegral_bind, lintegral_coin]
  Â· simp only [â†“reduceIte, Bool.false_eq_true]
  Â· fun_prop
  Â· apply isHom_cases (f := fun (p : Bool) _ â†¦ if p then Î¼ else Î½)
    Â· fun_prop
    Â· fun_prop

/-- Choosing with probability 1 returns the first measure. -/
@[simp]
lemma choose_one (Î¼ Î½ : ProbabilityMeasure A) : Î¼ â—ƒ 1 â–¹ Î½ = Î¼ := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_choose, Set.Icc.coe_one, ENNReal.ofReal_one, one_mul,
    unitInterval.symm_one, Set.Icc.coe_zero, ENNReal.ofReal_zero, zero_mul, add_zero]

/-- Choosing with probability 0 returns the second measure. -/
@[simp]
lemma choose_zero (Î¼ Î½ : ProbabilityMeasure A) : Î¼ â—ƒ 0 â–¹ Î½ = Î½ := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_choose, Set.Icc.coe_zero, ENNReal.ofReal_zero, zero_mul,
    unitInterval.symm_zero, Set.Icc.coe_one, ENNReal.ofReal_one, one_mul, zero_add]

/-- Choosing between the same measure returns the measure. -/
@[simp]
lemma choose_eq (p : I) (Î¼ : ProbabilityMeasure A) : Î¼ â—ƒpâ–¹ Î¼ = Î¼ := by
  rcases p with âŸ¨p, hpâŸ©
  simp only [Set.mem_Icc] at hp
  ext k hk
  simp (disch := fun_prop) [lintegral_choose, unitInterval.coe_symm_eq]
  simp only [hp, ENNReal.ofReal_sub, ENNReal.ofReal_one]

  wlog hkÎ¼ : âˆ«â» x, k x âˆ‚Î¼ â‰  âŠ¤
  Â· simp only [ne_eq, Decidable.not_not] at hkÎ¼
    simp only [hkÎ¼, ENNReal.add_eq_top]
    by_cases h : p > 0
    Â· simp only [ne_eq, ENNReal.ofReal_eq_zero, not_le, h, ENNReal.mul_top, true_or]
    Â· have : p = 0 := by grind
      subst this
      simp only [ENNReal.ofReal_zero, zero_mul, ENNReal.zero_ne_top, tsub_zero, one_mul, or_true]

  rw [ENNReal.sub_mul]
  Â· simp only [one_mul]
    apply add_tsub_cancel_of_le
    apply mul_le_of_le_one_left'
    simp only [ENNReal.ofReal_le_one, hp]
  Â· simp only [ne_eq, hkÎ¼, not_false_eq_true, implies_true]

/-- `choose` is commutative with symmetric probabilities. -/
lemma choose_comm (p : I) (Î¼ Î½ : ProbabilityMeasure A) : Î¼ â—ƒpâ–¹ Î½ = Î½ â—ƒÏƒ pâ–¹ Î¼ := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_choose, unitInterval.coe_symm_eq, unitInterval.symm_symm]
  rw [add_comm]

namespace unitInterval

/-- Helper function for `choose_assoc` -/
@[simps]
noncomputable def assocProdR (p q : I) : I where
  val := (Ïƒ p * q) / Ïƒ (p * q)
  property := by
    have hp := p.property
    have hq := q.property
    simp only [Set.mem_Icc] at hp hq
    simp only [unitInterval.coe_symm_eq, Set.Icc.coe_mul, Set.mem_Icc]
    by_cases hpq : (p : â„) < 1 âˆ¨ (q : â„) < 1
    Â· apply And.intro
      Â· rw [le_div_iffâ‚€]
        Â· nlinarith
        Â· cases hpq <;> nlinarith
      Â· rw [div_le_iffâ‚€]
        Â· nlinarith
        Â· cases hpq <;> nlinarith
    Â· simp only [not_or, not_lt] at hpq
      have hp' : (p : â„) = 1 := by grind
      have hq' : (q : â„) = 1 := by grind
      simp only [hp', sub_self, hq', mul_one, div_zero, le_refl, zero_le_one, and_self]

end unitInterval

/-- Associativity of `choose` with appropriate probability adjustments. -/
lemma choose_assoc {p q} {Î¼â‚ Î¼â‚‚ Î¼â‚ƒ : ProbabilityMeasure A}
    (hpâ‚ : 0 < p) (hpâ‚‚ : p < 1)
    (hqâ‚ : 0 < q) (hqâ‚‚ : q < 1)
    : (Î¼â‚ â—ƒpâ–¹ Î¼â‚‚) â—ƒqâ–¹ Î¼â‚ƒ
    = Î¼â‚ â—ƒp * qâ–¹ (Î¼â‚‚ â—ƒunitInterval.assocProdR p qâ–¹ Î¼â‚ƒ) := by
  ext k hk
  simp only [â† Subtype.coe_lt_coe, Set.Icc.coe_zero, Set.Icc.coe_one] at hpâ‚ hpâ‚‚ hqâ‚ hqâ‚‚
  have hâ‚ : 0 â‰¤ (p : â„) := p.property.1
  have hâ‚‚ : 0 â‰¤ (q : â„) := q.property.1
  have hâ‚ƒ : 0 â‰¤ 1 - (p : â„) := (Ïƒ p).property.1
  have hâ‚„ : 0 < 1 - p * (q : â„) := by
    simp only [sub_pos]
    apply lt_of_le_of_lt
    Â· change p * (q : â„) â‰¤ p * 1
      gcongr
    Â· simp only [mul_one, hpâ‚‚]
  have hâ‚… : 0 â‰¤ (1 - p) * q / (1 - p * (q : â„)) := by
    rw [le_div_iffâ‚€]
    Â· nlinarith
    Â· exact hâ‚„
  have hâ‚† : 0 â‰¤ 1 - p * (q : â„) := by grind

  have lemmaâ‚ : ENNReal.ofReal â†‘q * ENNReal.ofReal â†‘p = ENNReal.ofReal (â†‘p * â†‘q) := by
    simp only [ENNReal.ofReal_mul, hâ‚]
    ring_nf

  have lemmaâ‚‚
      : ENNReal.ofReal â†‘q * ENNReal.ofReal (1 - â†‘p)
      = ENNReal.ofReal (1 - â†‘p * â†‘q) * ENNReal.ofReal ((1 - â†‘p) * â†‘q / (1 - â†‘p * â†‘q)) := by
    simp only [
      â† ENNReal.ofReal_mul, mul_nonneg_iff_of_pos_left,
      ENNReal.ofReal_eq_ofReal_iff, hâ‚‚, hâ‚†, hqâ‚, hâ‚ƒ, hâ‚„, hâ‚…]
    rw [mul_div_cancelâ‚€]
    Â· ring_nf
    Â· grind

  have lemmaâ‚ƒ
      : ENNReal.ofReal (1 - â†‘q)
      = ENNReal.ofReal (1 - â†‘p * â†‘q) * ENNReal.ofReal (1 - (1 - â†‘p) * â†‘q / (1 - â†‘p * â†‘q)) := by
    simp only [â† ENNReal.ofReal_mul, hâ‚†, mul_sub]
    congr 1
    rw [mul_div_cancelâ‚€]
    Â· ring_nf
    Â· grind

  simp (disch := fun_prop) only [
    lintegral_choose, unitInterval.coe_symm_eq, mul_add, â† mul_assoc,
    add_assoc, Set.Icc.coe_mul, unitInterval.assocProdR_coe, lemmaâ‚, lemmaâ‚‚, lemmaâ‚ƒ]

namespace unitInterval

/-- Helper function for `choose_assoc'` -/
@[simps]
noncomputable def assocProdL (p q : I) : I where
  val := p / Ïƒ (Ïƒ p * Ïƒ q)
  property := by
    have âŸ¨hpâ‚, hpâ‚‚âŸ© := p.property
    have âŸ¨hqâ‚, hqâ‚‚âŸ© := q.property
    simp only [unitInterval.coe_symm_eq, Set.Icc.coe_mul, Set.mem_Icc]
    by_cases hpq : (p : â„) > 0 âˆ¨ (q : â„) > 0
    Â· apply And.intro
      Â· rw [le_div_iffâ‚€]
        Â· nlinarith
        Â· cases hpq <;> nlinarith
      Â· rw [div_le_iffâ‚€]
        Â· nlinarith
        Â· cases hpq <;> nlinarith
    Â· simp only [not_or, not_lt] at hpq
      have hp' : (p : â„) = 0 := by grind
      have hq' : (q : â„) = 0 := by grind
      simp only [hp', sub_zero, hq', mul_one, sub_self, div_zero, le_refl, zero_le_one, and_self]

@[simp]
lemma zero_lt_assocProdL (p q : I) : 0 < unitInterval.assocProdL p q â†” 0 < p := by
  simp only [
    â† Subtype.coe_lt_coe, Set.Icc.coe_zero, assocProdL_coe,
    unitInterval.coe_symm_eq, Set.Icc.coe_mul]
  apply Iff.intro
  Â· intro h
    by_contra! h'
    have : (p : â„) = 0 := le_antisymm h' p.property.1
    simp only [this, sub_zero, one_mul, sub_sub_cancel, zero_div, lt_self_iff_false] at h
  Â· intro h
    rw [lt_div_iffâ‚€]
    Â· simp only [zero_mul, h]
    Â· ring_nf
      suffices 0 < q * (1 - p) + (p : â„) by nlinarith
      apply lt_of_lt_of_le
      Â· apply h
      Â· simp only [le_add_iff_nonneg_left]
        exact (q * Ïƒ p).property.1

@[simp]
lemma lt_one_iff_ge_zero (p) : Ïƒ p < 1 â†” 0 < p := by
  simp only [â† Subtype.coe_lt_coe]
  simp only [
    unitInterval.coe_symm_eq, Set.Icc.coe_one, sub_lt_self_iff,
    unitInterval.coe_pos, Set.Icc.coe_zero]

@[simp]
lemma zero_lt_iff_lt_one (p) : 0 < Ïƒ p â†” p < 1 := by
  simp only [â† Subtype.coe_lt_coe]
  simp only [
    Set.Icc.coe_zero, unitInterval.coe_symm_eq, sub_pos,
    unitInterval.coe_lt_one, Set.Icc.coe_one]

@[simp]
lemma zero_le_mul_iff (p q : I) : 0 < p * q â†” 0 < p âˆ§ 0 < q := by
  simp only [â† Subtype.coe_lt_coe, Set.Icc.coe_zero, Set.Icc.coe_mul]
  apply Iff.intro
  Â· intro h
    apply And.intro
    Â· apply lt_of_lt_of_le
      Â· change 0 < p * (q : â„)
        apply h
      Â· trans (p : â„) * 1
        Â· gcongr 1
          Â· exact p.property.1
          Â· exact q.property.2
        Â· simp only [mul_one, le_refl]
    Â· apply lt_of_lt_of_le
      Â· change 0 < p * (q : â„)
        apply h
      Â· trans 1 * (q : â„)
        Â· gcongr 1
          Â· exact q.property.1
          Â· exact p.property.2
        Â· simp only [one_mul, le_refl]
  Â· rintro âŸ¨hâ‚, hâ‚‚âŸ©
    apply lt_of_le_of_lt
    Â· change 0 â‰¤ 0 * (q : â„)
      simp only [zero_mul, le_refl]
    Â· gcongr 1

@[simp]
lemma mul_lt_one_iff (p q : I) : p * q < 1 â†” p < 1 âˆ¨ q < 1 := by
  simp only [â† Subtype.coe_lt_coe, Set.Icc.coe_mul, Set.Icc.coe_one]
  by_contra! h
  cases h with
  | inl h =>
    rcases h with âŸ¨hâ‚, hâ‚‚, hâ‚ƒâŸ©
    replace hâ‚ƒ := le_antisymm q.property.2 hâ‚ƒ
    replace hâ‚‚ := le_antisymm p.property.2 hâ‚‚
    simp only [hâ‚‚, hâ‚ƒ, mul_one, lt_self_iff_false] at hâ‚
  | inr h =>
    rcases h with âŸ¨hâ‚, hâ‚‚âŸ©
    have : 1 â‰¤ (p : â„) := by
      trans p * (q : â„)
      Â· exact hâ‚
      Â· trans (p : â„) * 1
        Â· gcongr 1
          Â· exact p.property.1
          Â· exact q.property.2
        Â· simp only [mul_one, le_refl]
    have : 1 â‰¤ (q : â„) := by
      trans p * (q : â„)
      Â· exact hâ‚
      Â· trans 1 * (q : â„)
        Â· gcongr 1
          Â· exact q.property.1
          Â· exact p.property.2
        Â· simp only [one_mul, le_refl]
    grind

end unitInterval

/-- Associativity of `choose` with appropriate probability adjustments (other direction). -/
lemma choose_assoc' {p q} {Î¼â‚ Î¼â‚‚ Î¼â‚ƒ : ProbabilityMeasure A}
    (hpâ‚ : 0 < p) (hpâ‚‚ : p < 1)
    (hqâ‚ : 0 < q) (hqâ‚‚ : q < 1)
    : Î¼â‚ â—ƒpâ–¹ (Î¼â‚‚ â—ƒqâ–¹ Î¼â‚ƒ)
    = (Î¼â‚ â—ƒunitInterval.assocProdL p qâ–¹ Î¼â‚‚) â—ƒÏƒ (Ïƒ p * Ïƒ q)â–¹ Î¼â‚ƒ := by
  have lemmaâ‚€ : 0 < p - p * q + (q : â„) := by
    suffices 0 < q * (1 - p) + (p : â„) by nlinarith
    apply lt_of_lt_of_le
    Â· exact hpâ‚
    Â· simp only [le_add_iff_nonneg_left]
      exact (q * Ïƒ p).property.1

  have lemmaâ‚ : unitInterval.assocProdL p q * Ïƒ (Ïƒ p * Ïƒ q) = p := by
    ext : 1
    simp only [Set.Icc.coe_mul, unitInterval.assocProdL_coe, unitInterval.coe_symm_eq]
    apply mul_left_cancelâ‚€ (a := (1 - (1 - p) * (1 - (q : â„))))
    Â· ring_nf
      grind
    Â· rw [div_mul_cancelâ‚€]
      ring_nf
      grind

  have lemmaâ‚‚ : unitInterval.assocProdR (unitInterval.assocProdL p q) (Ïƒ (Ïƒ p * Ïƒ q)) = q := by
    ext : 1
    simp only [
      unitInterval.assocProdR_coe, unitInterval.coe_symm_eq,
      unitInterval.assocProdL_coe, Set.Icc.coe_mul]
    generalize hs : (1 - (1 - p) * (1 - (q : â„))) = s
    generalize hr : (1 - p / s * s) = r
    have hs' : 0 < s := by
      rw [â† hs]
      ring_nf
      grind
    replace hr : (1 - p) = r := by
      rw [â† hr, div_mul_cancelâ‚€]
      grind
    have hr' : 0 < r := by
      simp only [â† hr, sub_pos, unitInterval.coe_lt_one, hpâ‚‚]
    simp only [sub_mul, one_mul, sub_div]
    rw [div_mul_cancelâ‚€]
    Â· simp only [â† sub_div]
      apply mul_right_cancelâ‚€ (b := r)
      Â· grind
      Â· rw [div_mul_cancelâ‚€]
        Â· rw [â† hr, â† hs]
          ring_nf
        Â· grind
    Â· grind

  rw [choose_assoc, lemmaâ‚, lemmaâ‚‚]
  Â· simp only [unitInterval.zero_lt_assocProdL, hpâ‚]
  Â· rw [â† Subtype.coe_lt_coe]
    simp only [unitInterval.assocProdL_coe, unitInterval.coe_symm_eq, Set.Icc.coe_mul,
      Set.Icc.coe_one]
    rw [div_lt_iffâ‚€]
    Â· simp only [one_mul]
      apply lt_of_add_lt_add_right (a := p * (q : â„))
      ring_nf
      simp only [
        add_lt_add_iff_left, unitInterval.coe_pos, mul_lt_iff_lt_one_left,
        unitInterval.coe_lt_one, hqâ‚, hpâ‚‚]
    Â· ring_nf
      exact lemmaâ‚€
  Â· simp only [unitInterval.zero_lt_iff_lt_one, unitInterval.mul_lt_one_iff,
    unitInterval.lt_one_iff_ge_zero, or_self, hpâ‚, hqâ‚]
  Â· simp only [unitInterval.lt_one_iff_ge_zero, unitInterval.zero_le_mul_iff,
    unitInterval.zero_lt_iff_lt_one, hpâ‚‚, hqâ‚‚, and_self]

/-- `bind` distributes over `choose`. -/
@[simp]
lemma bind_choose
    {f : A â†’ ProbabilityMeasure B} (hf : IsHom f)
    (p : unitInterval) (Î¼ Î½ : ProbabilityMeasure A)
    : bind f (Î¼ â—ƒpâ–¹ Î½) = bind f Î¼ â—ƒpâ–¹ bind f Î½ := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_bind, lintegral_choose, unitInterval.coe_symm_eq]

/-- `map` distributes over `choose`. -/
@[simp]
lemma map_choose
    {f : A â†’ B} (hf : IsHom f)
    (p : unitInterval) (Î¼ Î½ : ProbabilityMeasure A)
    : map f (Î¼ â—ƒpâ–¹ Î½) = map f Î¼ â—ƒpâ–¹ map f Î½ := by
  apply bind_choose
  fun_prop

/-- `choose` commutes with `bind`. -/
@[simp]
lemma choose_bind
    {f : A â†’ ProbabilityMeasure B} (hf : IsHom f)
    {g : A â†’ ProbabilityMeasure B} (hg : IsHom g)
    (p : unitInterval) (Î¼ : ProbabilityMeasure A)
    : bind (fun x â†¦ f x â—ƒpâ–¹ g x) Î¼ = bind f Î¼ â—ƒpâ–¹ bind g Î¼ := by
  ext k hk
  simp (disch := fun_prop) only [
    lintegral_bind, lintegral_choose, unitInterval.coe_symm_eq,
    lintegral_add_left, lintegral_mul_left]

end QuasiBorelSpace.ProbabilityMeasure
