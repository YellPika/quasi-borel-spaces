import QuasiBorelSpaces.ENNReal
import QuasiBorelSpaces.PreProbabilityMeasure
import QuasiBorelSpaces.SeparatesPoints

open MeasureTheory
open scoped unitInterval

namespace QuasiBorelSpace

variable {A B C A' B' C' : Type*}
  [QuasiBorelSpace A] [QuasiBorelSpace B] [QuasiBorelSpace C]
  [QuasiBorelSpace A'] [QuasiBorelSpace B'] [QuasiBorelSpace C']

/-! ## Basic Definitions -/

/-- The type of _(quasi-borel) probability measures_. -/
structure ProbabilityMeasure (A : Type*) [QuasiBorelSpace A] where
  private fromQuotient ::
  private val : Quotient (PreProbabilityMeasure.setoid A)

namespace ProbabilityMeasure

/-- Constructs a `ProbabilityMeasure` from a `PreProbabilityMeasure`. -/
def mk (Î¼ : PreProbabilityMeasure A) : ProbabilityMeasure A := âŸ¨âŸ¦Î¼âŸ§âŸ©

@[simp]
lemma mk_eq_iff (Î¼ Î½ : PreProbabilityMeasure A) : mk Î¼ = mk Î½ â†” Î¼ â‰ˆ Î½ := by
  simp only [mk, fromQuotient.injEq, Quotient.eq, PreProbabilityMeasure.setoid_r]

@[induction_eliminator, cases_eliminator]
lemma inductionOn
    {motive : ProbabilityMeasure A â†’ Prop} (Î¼ : ProbabilityMeasure A)
    (mk : (Î¼ : PreProbabilityMeasure A) â†’ motive (mk Î¼))
    : motive Î¼ := by
  rcases Î¼ with âŸ¨Î¼âŸ©
  induction Î¼ using Quotient.inductionOn with | h Î¼ =>
  rcases Î¼ with âŸ¨Î±, hÎ±, Î¼âŸ©
  apply mk

/--
Converts a `ProbabilityMeasure` to the underlying `PreProbabilityMeasure`. This
may or may not be the one passed to `mk`, but will always be equivalent
(`(mk Î¼).toPreProbabilityMeasure â‰ˆ Î¼`).
-/
noncomputable def toPreProbabilityMeasure (Î¼ : ProbabilityMeasure A)
    : PreProbabilityMeasure A :=
  Î¼.val.out

lemma toPreProbabilityMeasure_mk (Î¼ : PreProbabilityMeasure A)
    : toPreProbabilityMeasure (mk Î¼) â‰ˆ Î¼ := by
  apply Quotient.exact
  simp only [toPreProbabilityMeasure, mk, Quotient.out_eq]

lemma nonempty (Î¼ : ProbabilityMeasure A) : Nonempty A := Î¼.toPreProbabilityMeasure.nonempty

/-! ## `QuasiBorelSpace` Instance -/

instance : QuasiBorelSpace (ProbabilityMeasure A) := lift toPreProbabilityMeasure

@[simp, fun_prop]
lemma isHom_toPreProbabilityMeasure : IsHom (toPreProbabilityMeasure (A := A)) := by
  apply isHom_of_lift

@[simp, fun_prop]
lemma isHom_mk : IsHom (mk (A := A)) := by
  rw [isHom_to_lift, PreProbabilityMeasure.isHom_congr toPreProbabilityMeasure_mk]
  fun_prop

/-! ## Integrals -/

/-- The integral of a function over a `ProbabilityMeasure`. -/
noncomputable def lintegral (k : A â†’ ENNReal) (Î¼ : ProbabilityMeasure A) : ENNReal :=
  Î¼.toPreProbabilityMeasure.lintegral k

@[simp]
lemma lintegral_mk
    {k : A â†’ ENNReal} (hk : IsHom k) (Î¼ : PreProbabilityMeasure A)
    : lintegral k (mk Î¼) = Î¼.lintegral k := by
  apply PreProbabilityMeasure.lintegral_congr hk
  apply toPreProbabilityMeasure_mk

@[simp]
lemma lintegral_toPreProbabilityMeasure
    (Î¼ : ProbabilityMeasure A) (k : A â†’ ENNReal)
    : Î¼.toPreProbabilityMeasure.lintegral k = Î¼.lintegral k := by
  rfl

@[ext]
lemma ext
    {Î¼â‚ Î¼â‚‚ : ProbabilityMeasure A}
    (hÎ¼ : âˆ€ {k}, IsHom k â†’ Î¼â‚.lintegral k = Î¼â‚‚.lintegral k)
    : Î¼â‚ = Î¼â‚‚ := by
  cases Î¼â‚ with | mk Î¼ =>
  cases Î¼â‚‚ with | mk Î½ =>
  simp only [mk_eq_iff, PreProbabilityMeasure.equiv_def]
  intro k hk
  specialize hÎ¼ hk
  simp only [hk, lintegral_mk] at hÎ¼
  exact hÎ¼

@[fun_prop]
lemma isHom_lintegral
    {k : A â†’ B â†’ ENNReal} (hk : IsHom fun (x, y) â†¦ k x y)
    {f : A â†’ ProbabilityMeasure B} (hf : IsHom f)
    : IsHom (fun x â†¦ lintegral (k x) (f x)) := by
  simp (disch := fun_prop) only [â† lintegral_toPreProbabilityMeasure]
  fun_prop

lemma lintegral_add
    {f : A â†’ ENNReal} (hf : IsHom f)
    {g : A â†’ ENNReal} (hg : IsHom g)
    (Î¼ : ProbabilityMeasure A)
    : lintegral (f + g) Î¼ = lintegral f Î¼ + lintegral g Î¼ := by
  cases Î¼ with | mk Î¼ =>
  have : IsHom (f + g) := by
    change IsHom (fun x â†¦ f x + g x)
    fun_prop
  simp (disch := fun_prop) only [lintegral_mk]
  apply PreProbabilityMeasure.lintegral_add_left
  exact hf

lemma lintegral_add'
    {f : A â†’ ENNReal} (hf : IsHom f)
    {g : A â†’ ENNReal} (hg : IsHom g)
    (Î¼ : ProbabilityMeasure A)
    : lintegral (fun x â†¦ f x + g x) Î¼ = lintegral f Î¼ + lintegral g Î¼ := by
  apply lintegral_add hf hg

lemma lintegral_mul_left
    (c : ENNReal) {f : A â†’ ENNReal} (hf : IsHom f) (Î¼ : ProbabilityMeasure A)
    : lintegral (fun x â†¦ c * f x) Î¼ = c * lintegral f Î¼ := by
  cases Î¼ with | mk Î¼ =>
  simp (disch := fun_prop) only [lintegral_mk, PreProbabilityMeasure.lintegral_mul_left]

lemma lintegral_mul_right
    (c : ENNReal) {f : A â†’ ENNReal} (hf : IsHom f) (Î¼ : ProbabilityMeasure A)
    : lintegral (fun x â†¦ f x * c) Î¼ = lintegral f Î¼ * c := by
  cases Î¼ with | mk Î¼ =>
  simp (disch := fun_prop) only [lintegral_mk, PreProbabilityMeasure.lintegral_mul_right]

@[simp]
lemma lintegral_const (c : ENNReal) (Î¼ : ProbabilityMeasure A) : lintegral (fun _ â†¦ c) Î¼ = c := by
  cases Î¼ with | mk Î¼ =>
  simp (disch := fun_prop) only [lintegral_mk, PreProbabilityMeasure.lintegral_const]

@[simp]
lemma lintegral_mono
    {f g : A â†’ ENNReal} (h : f â‰¤ g) (Î¼ : ProbabilityMeasure A)
    : lintegral f Î¼ â‰¤ lintegral g Î¼ := by
  unfold lintegral
  apply PreProbabilityMeasure.lintegral_mono h

lemma lintegral_iSup
    (f : â„• â†’ A â†’ ENNReal) (hfâ‚ : Monotone f) (hfâ‚‚ : âˆ€ n, IsHom (f n)) (Î¼ : ProbabilityMeasure A)
    : â¨†n, lintegral (f n) Î¼ = lintegral (â¨†n, f n) Î¼ := by
  unfold lintegral
  apply PreProbabilityMeasure.lintegral_iSup f hfâ‚ hfâ‚‚

lemma lintegral_finset_sum {A}
    (s : Finset A) {f : A â†’ B â†’ ENNReal}
    (hf : âˆ€ b âˆˆ s, IsHom (f b)) (Î¼ : ProbabilityMeasure B) :
    lintegral (fun a â†¦ âˆ‘ b âˆˆ s, f b a) Î¼ = âˆ‘ b âˆˆ s, lintegral (f b) Î¼ := by
  unfold lintegral
  apply PreProbabilityMeasure.lintegral_finset_sum s hf

/-! ## Measures -/

noncomputable instance : FunLike (ProbabilityMeasure A) (Set A) ENNReal where
  coe Î¼ s := Î¼.toPreProbabilityMeasure.measureOf s
  coe_injective' := by
    intro Î¼â‚ Î¼â‚‚ h
    induction Î¼â‚ with | mk Î¼â‚ =>
    induction Î¼â‚‚ with | mk Î¼â‚‚ =>
    simp only [mk_eq_iff]
    rw [PreProbabilityMeasure.equiv_def']
    intro p hp
    grw [â†toPreProbabilityMeasure_mk Î¼â‚, â†toPreProbabilityMeasure_mk Î¼â‚‚]
    Â· apply congr_fun h
    Â· fun_prop
    Â· fun_prop

instance : OuterMeasureClass (ProbabilityMeasure A) A where
  measure_empty _ := by
    simp only [DFunLike.coe, PreProbabilityMeasure.measureOf_empty]
  measure_mono _ _ _ h := by
    apply PreProbabilityMeasure.measureOf_mono _ h
  measure_iUnion_nat_le _ _ _ := by
    apply PreProbabilityMeasure.measureOf_iUnion_le

/-! ## Point Separation -/

instance : SeparatesPoints (ProbabilityMeasure A) where
  separates Î¼â‚ Î¼â‚‚ h := by
    ext k
    apply h _
    Â· apply isHom_comp'
      Â· rw [isHom_def]
        intro Ï† hÏ†
        simp only [isHom_ofMeasurableSpace] at âŠ¢ hÏ†
        have : MeasurableSet { x | x âˆˆ Ï† â»Â¹' ({lintegral k Î¼â‚} : Set _) } := by
          apply hÏ†
          apply measurableSet_eq
        simp only [Set.mem_preimage, Set.mem_singleton_iff, measurableSet_setOf] at this
        grind
      Â· fun_prop
    Â· rfl

/-! ## Operations -/

/-! ### `unit` -/

/-- The monadic `unit` operation. -/
noncomputable def unit (x : A) : ProbabilityMeasure A := mk (PreProbabilityMeasure.unit x)

@[simp, fun_prop]
lemma isHom_unit : IsHom (unit (A := A)) := by
  unfold unit
  fun_prop

@[simp]
lemma lintegral_unit {k : A â†’ ENNReal} (hk : IsHom k) (x : A) : lintegral k (unit x) = k x := by
  simp only [unit, hk, lintegral_mk, PreProbabilityMeasure.lintegral_unit]

@[simp]
lemma unit_injective [SeparatesPoints A] : Function.Injective (unit (A := A)) := by
  intro x y h
  simp only [ProbabilityMeasure.ext_iff] at h
  apply separatesPoints_def
  intro p hp hx
  classical
  have : IsHom fun x â†¦ if p x then 1 else (0 : ENNReal) := by
    apply Prop.isHom_ite <;> fun_prop
  specialize h this
  simp (disch := fun_prop) only [
    lintegral_unit, hx, â†“reduceIte, left_eq_ite_iff, one_ne_zero,
    imp_false, Decidable.not_not] at h
  exact h

@[simp]
lemma unit_inj [SeparatesPoints A] (x y : A) : unit x = unit y â†” x = y := by
  apply Iff.intro
  Â· apply unit_injective
  Â· grind

lemma separatesPoints_iff_unit_injective
    : SeparatesPoints A â†” Function.Injective (unit (A := A)) := by
  apply Iff.intro
  Â· intro _
    apply unit_injective
  Â· intro h
    constructor
    intro x y h'
    apply h
    ext k hk
    apply h'
    Â· simp only [hk, lintegral_unit]
      apply isHom_comp' ?_ hk
      rw [isHom_def]
      intro Ï† hÏ†
      simp only [isHom_ofMeasurableSpace] at âŠ¢ hÏ†
      apply MeasurableSet.mem
      have : MeasurableSet fun r â†¦ r âˆˆ (Ï† â»Â¹' ({k x} : Set _)) := by
        apply hÏ†
        apply measurableSet_eq
      grind
    Â· rfl

/-! ### `bind` -/

/-- The monadic `bind` operation. -/
noncomputable def bind
    (f : A â†’ ProbabilityMeasure B) (Î¼ : ProbabilityMeasure A)
    : ProbabilityMeasure B :=
  mk (PreProbabilityMeasure.bind (fun x â†¦ (f x).toPreProbabilityMeasure) Î¼.toPreProbabilityMeasure)

@[simp, fun_prop]
lemma isHom_bind {f : A â†’ ProbabilityMeasure B} (hf : IsHom f) : IsHom (bind f) := by
  unfold bind
  fun_prop

@[simp]
lemma lintegral_bind
    {k : B â†’ ENNReal} (hk : IsHom k)
    {f : A â†’ ProbabilityMeasure B} (hf : IsHom f)
    (Î¼ : ProbabilityMeasure A)
    : lintegral k (bind f Î¼) = lintegral (fun x â†¦ lintegral k (f x)) Î¼ := by
  cases Î¼ with | mk Î¼ =>
  have : IsHom fun x â†¦ lintegral k (f x) := by fun_prop
  simp only [bind, hk, lintegral_mk, this]
  rw [PreProbabilityMeasure.lintegral_bind]
  Â· apply toPreProbabilityMeasure_mk
    fun_prop
  Â· fun_prop
  Â· fun_prop

@[simp]
lemma bind_unit {f : A â†’ ProbabilityMeasure B} (hf : IsHom f) (x : A) : bind f (unit x) = f x := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_bind, lintegral_unit]

@[simp]
lemma unit_bind (Î¼ : ProbabilityMeasure A) : bind unit Î¼ = Î¼ := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_bind, lintegral_unit]

@[simp]
lemma bind_bind
    {f : B â†’ ProbabilityMeasure C} (hf : IsHom f)
    {g : A â†’ ProbabilityMeasure B} (hg : IsHom g)
    (Î¼ : ProbabilityMeasure A)
    : bind f (bind g Î¼) = bind (fun x â†¦ bind f (g x)) Î¼ := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_bind]

/-! ### `map` -/

/-- The functorial `map` operation. -/
noncomputable def map (f : A â†’ B) (Î¼ : ProbabilityMeasure A) : ProbabilityMeasure B :=
  bind (fun x â†¦ unit (f x)) Î¼

@[fun_prop]
lemma isHom_map {f : A â†’ B} (hf : IsHom f) : IsHom (map f) := by
  unfold map
  fun_prop

@[simp]
lemma lintegral_map
    {k : B â†’ ENNReal} (hk : IsHom k)
    {f : A â†’ B} (hf : IsHom f) (Î¼ : ProbabilityMeasure A)
    : lintegral k (map f Î¼) = lintegral (fun x â†¦ k (f x)) Î¼ := by
  simp (disch := fun_prop) only [map, lintegral_bind, lintegral_unit]

@[simp]
lemma map_id : map (fun x : A â†¦ x) = id := by
  funext Î¼
  simp only [map, unit_bind, id_eq]

@[simp]
lemma map_id' : map (A := A) id = id := map_id

@[simp]
lemma map_map
    {f : B â†’ C} (hf : IsHom f)
    {g : A â†’ B} (hg : IsHom g)
    (Î¼ : ProbabilityMeasure A)
    : map f (map g Î¼) = map (fun x â†¦ f (g x)) Î¼ := by
  simp (disch := fun_prop) only [map, bind_bind, bind_unit]

@[simp]
lemma map_bind
    {f : B â†’ C} (hf : IsHom f)
    {g : A â†’ ProbabilityMeasure B} (hg : IsHom g)
    (Î¼ : ProbabilityMeasure A)
    : map f (bind g Î¼) = bind (fun x â†¦ map f (g x)) Î¼ := by
  simp (disch := fun_prop) only [map, bind_bind]

@[simp]
lemma bind_map
    {f : B â†’ ProbabilityMeasure C} (hf : IsHom f)
    {g : A â†’ B} (hg : IsHom g)
    (Î¼ : ProbabilityMeasure A)
    : bind f (map g Î¼) = bind (fun x â†¦ f (g x)) Î¼ := by
  simp (disch := fun_prop) only [map, bind_bind, bind_unit]

@[simp]
lemma map_unit {f : A â†’ B} (hf : IsHom f) (x : A) : map f (unit x) = unit (f x) := by
  simp (disch := fun_prop) only [map, bind_unit]

@[simp]
lemma bind_unit_eq_map {f : A â†’ B} : bind (fun x â†¦ unit (f x)) = map f := by
  funext Î¼
  simp only [map]

/-! ### `str` -/

/-- The functorial `str`ength operation. -/
noncomputable def str (x : A) (Î¼ : ProbabilityMeasure B) : ProbabilityMeasure (A Ã— B) :=
  mk (PreProbabilityMeasure.str x Î¼.toPreProbabilityMeasure)

@[simp]
lemma lintegral_str
    {k : A Ã— B â†’ ENNReal} (hk : IsHom k)
    (x : A) (Î¼ : ProbabilityMeasure B)
    : lintegral k (str x Î¼) = lintegral (fun y â†¦ k (x, y)) Î¼ := by
  cases Î¼ with | mk Î¼ =>
  simp (disch := fun_prop) only [
    str, lintegral_mk, PreProbabilityMeasure.lintegral_str,
    lintegral_toPreProbabilityMeasure]

@[simp, local fun_prop]
lemma isHom_str : IsHom (Function.uncurry (str (A := A) (B := B))) := by
  unfold Function.uncurry
  simp only [str]
  fun_prop

@[fun_prop]
lemma isHom_str'
    {f : A â†’ B} (hf : IsHom f)
    {g : A â†’ ProbabilityMeasure C} (hg : IsHom g)
    : IsHom (fun x â†¦ str (f x) (g x)) := by
  fun_prop

@[simp]
lemma str_eq_map (x : A) (Î¼ : ProbabilityMeasure B) : str x Î¼ = map (x, Â·) Î¼ := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_str, lintegral_map]

@[fun_prop]
lemma isHom_bind'
    {f : C â†’ A â†’ ProbabilityMeasure B} (hf : IsHom (Function.uncurry f))
    {g : C â†’ ProbabilityMeasure A} (hg : IsHom g)
    : IsHom (fun x â†¦ bind (f x) (g x)) := by
  have hf' : âˆ€x, IsHom (f x) := by fun_prop
  have {x}
      : bind (f x) (g x)
      = bind (fun x : (A â†’ğ’’ ProbabilityMeasure B) Ã— A â†¦ x.1 x.2) (str âŸ¨f x, hf' xâŸ© (g x)) := by
    simp only [
      str_eq_map, QuasiBorelHom.isHom_eval, Prod.isHom_iff, isHom_const,
      isHom_id', and_self, bind_map, QuasiBorelHom.coe_mk]
  simp only [this]
  fun_prop

@[fun_prop]
lemma isHom_map'
    {f : C â†’ A â†’ B} (hf : IsHom (Function.uncurry f))
    {g : C â†’ ProbabilityMeasure A} (hg : IsHom g)
    : IsHom (fun x â†¦ map (f x) (g x)) := by
  unfold map
  fun_prop

example (Î¼ : ProbabilityMeasure A) : str () Î¼ = map ((), Â·) Î¼ := by
  simp only [str_eq_map]

example (x : A) (y : B) : str x (unit y) = unit (x, y) := by
  simp only [str_eq_map, Prod.isHom_iff, isHom_const, isHom_id', and_self, map_unit]

example
    {f : A â†’ A'} (hf : IsHom f)
    {g : B â†’ B'} (hg : IsHom g)
    (x : A) (Î¼ : ProbabilityMeasure B)
    : map (Prod.map f g) (str x Î¼) = str (f x) (map g Î¼) := by
  simp (disch := fun_prop) only [str_eq_map, map_map, Prod.map_apply]

example
    (x : A) (Î¼ : ProbabilityMeasure (ProbabilityMeasure B))
    : bind (Function.uncurry str) (str x Î¼) = str x (bind id Î¼) := by
  simp only [
    str_eq_map, isHom_str, Prod.isHom_iff, isHom_const, isHom_id', and_self,
    bind_map, Function.uncurry_apply_pair, isHom_id, map_bind, id_eq]

/-! ### `coin` -/

/-- The Bernoulli measure. -/
noncomputable def coin (p : I) : ProbabilityMeasure Bool :=
  mk (.coin p)

@[simp]
lemma lintegral_coin
    (k : Bool â†’ ENNReal) (p : I)
    : lintegral k (coin p) = ENNReal.ofReal p * k true + ENNReal.ofReal (1 - p) * k false := by
  simp only [coin, isHom_of_discrete_countable, lintegral_mk, PreProbabilityMeasure.lintegral_coin]

/-! ### `choose` -/

/-- Probabilistic choice. -/
noncomputable def choose (p : I) (Î¼ Î½ : ProbabilityMeasure A) : ProbabilityMeasure A :=
  bind (fun b â†¦ if b then Î¼ else Î½) (coin p)

@[inherit_doc choose]
scoped notation:65 Î¼ " â—ƒ" p "â–¹ " Î½:66 => choose p Î¼ Î½

@[fun_prop]
lemma isHom_choose
    (p : I)
    {f : A â†’ ProbabilityMeasure B} (hf : IsHom f)
    {g : A â†’ ProbabilityMeasure B} (hg : IsHom g)
    : IsHom (fun x â†¦ f x â—ƒpâ–¹ g x) := by
  simp only [choose]
  apply isHom_bind'
  Â· unfold Function.uncurry
    dsimp only
    apply isHom_cases (ix := Prod.snd) (f := fun (b : Bool) x â†¦ if b then f x.1 else g x.1)
    Â· fun_prop
    Â· intro b
      cases b <;>
      Â· simp only [Bool.false_eq_true, â†“reduceIte]
        fun_prop
  Â· fun_prop

@[simp]
lemma lintegral_choose
    {k : A â†’ ENNReal} (hk : IsHom k)
    (p : I) (Î¼ Î½ : ProbabilityMeasure A)
    : lintegral k (Î¼ â—ƒpâ–¹ Î½)
    = ENNReal.ofReal p * lintegral k Î¼ + ENNReal.ofReal (Ïƒ p) * lintegral k Î½ := by
  simp (disch := fun_prop) only [choose, unitInterval.coe_symm_eq]
  rw [lintegral_bind, lintegral_coin]
  Â· simp only [â†“reduceIte, Bool.false_eq_true]
  Â· fun_prop
  Â· apply isHom_cases (f := fun (p : Bool) _ â†¦ if p then Î¼ else Î½)
    Â· fun_prop
    Â· fun_prop

@[simp]
lemma choose_one (Î¼ Î½ : ProbabilityMeasure A) : Î¼ â—ƒ 1 â–¹ Î½ = Î¼ := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_choose, Set.Icc.coe_one, ENNReal.ofReal_one, one_mul,
    unitInterval.symm_one, Set.Icc.coe_zero, ENNReal.ofReal_zero, zero_mul, add_zero]

@[simp]
lemma choose_zero (Î¼ Î½ : ProbabilityMeasure A) : Î¼ â—ƒ 0 â–¹ Î½ = Î½ := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_choose, Set.Icc.coe_zero, ENNReal.ofReal_zero, zero_mul,
    unitInterval.symm_zero, Set.Icc.coe_one, ENNReal.ofReal_one, one_mul, zero_add]

@[simp]
lemma choose_eq (p : I) (Î¼ : ProbabilityMeasure A) : Î¼ â—ƒpâ–¹ Î¼ = Î¼ := by
  rcases p with âŸ¨p, hpâŸ©
  simp only [Set.mem_Icc] at hp
  ext k hk
  simp (disch := fun_prop) [lintegral_choose, unitInterval.coe_symm_eq]
  simp only [hp, ENNReal.ofReal_sub, ENNReal.ofReal_one]

  wlog hkÎ¼ : lintegral k Î¼ â‰  âŠ¤
  Â· simp only [ne_eq, Decidable.not_not] at hkÎ¼
    simp only [hkÎ¼, ENNReal.add_eq_top]
    by_cases h : p > 0
    Â· simp only [ne_eq, ENNReal.ofReal_eq_zero, not_le, h, ENNReal.mul_top, true_or]
    Â· have : p = 0 := by grind
      subst this
      simp only [ENNReal.ofReal_zero, zero_mul, ENNReal.zero_ne_top, tsub_zero, one_mul, or_true]

  rw [ENNReal.sub_mul]
  Â· simp only [one_mul]
    apply add_tsub_cancel_of_le
    apply mul_le_of_le_one_left'
    simp only [ENNReal.ofReal_le_one, hp]
  Â· simp only [ne_eq, hkÎ¼, not_false_eq_true, implies_true]

lemma choose_comm (p : I) (Î¼ Î½ : ProbabilityMeasure A) : Î¼ â—ƒpâ–¹ Î½ = Î½ â—ƒÏƒ pâ–¹ Î¼ := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_choose, unitInterval.coe_symm_eq, unitInterval.symm_symm]
  rw [add_comm]

private lemma choose_assoc_bound {p q : I}
    (hpâ‚ : 0 < p) (hpâ‚‚ : p < 1)
    (hqâ‚ : 0 < q) (hqâ‚‚ : q < 1)
    : (Ïƒ p * q : â„) / Ïƒ (p * q) âˆˆ I := by
  have hp := p.property
  have hq := q.property
  simp only [unitInterval.coe_symm_eq, Set.mem_Icc, Set.Icc.coe_mul]
  have hp_pos : 0 < (p : â„) := by simpa using hpâ‚
  have hq_pos : 0 < (q : â„) := by simpa using hqâ‚
  have hp_lt : (p : â„) < 1 := by simpa using hpâ‚‚
  have hq_lt : (q : â„) < 1 := by simpa using hqâ‚‚
  have h_denom_pos : 0 < 1 - (p : â„) * â†‘q := by nlinarith
  constructor
  Â· apply div_nonneg <;> nlinarith
  Â· rw [div_le_one h_denom_pos]
    nlinarith

lemma choose_assoc {p q} {Î¼â‚ Î¼â‚‚ Î¼â‚ƒ : ProbabilityMeasure A}
    (hpâ‚ : 0 < p) (hpâ‚‚ : p < 1)
    (hqâ‚ : 0 < q) (hqâ‚‚ : q < 1)
    : (Î¼â‚ â—ƒpâ–¹ Î¼â‚‚) â—ƒqâ–¹ Î¼â‚ƒ
    = Î¼â‚ â—ƒp * qâ–¹ (Î¼â‚‚ â—ƒâŸ¨_, choose_assoc_bound hpâ‚ hpâ‚‚ hqâ‚ hqâ‚‚âŸ©â–¹ Î¼â‚ƒ) := by
  sorry

@[simp]
lemma bind_choose
    {f : A â†’ ProbabilityMeasure B} (hf : IsHom f)
    (p : unitInterval) (Î¼ Î½ : ProbabilityMeasure A)
    : bind f (Î¼ â—ƒpâ–¹ Î½) = bind f Î¼ â—ƒpâ–¹ bind f Î½ := by
  ext k hk
  simp (disch := fun_prop) only [lintegral_bind, lintegral_choose, unitInterval.coe_symm_eq]

@[simp]
lemma map_choose
    {f : A â†’ B} (hf : IsHom f)
    (p : unitInterval) (Î¼ Î½ : ProbabilityMeasure A)
    : map f (Î¼ â—ƒpâ–¹ Î½) = map f Î¼ â—ƒpâ–¹ map f Î½ := by
  apply bind_choose
  fun_prop

@[simp]
lemma choose_bind
    {f : A â†’ ProbabilityMeasure B} (hf : IsHom f)
    {g : A â†’ ProbabilityMeasure B} (hg : IsHom g)
    (p : unitInterval) (Î¼ : ProbabilityMeasure A)
    : bind (fun x â†¦ f x â—ƒpâ–¹ g x) Î¼ = bind f Î¼ â—ƒpâ–¹ bind g Î¼ := by
  ext k hk
  simp (disch := fun_prop) only [
    lintegral_bind, lintegral_choose, unitInterval.coe_symm_eq,
    lintegral_add', lintegral_mul_left]

end QuasiBorelSpace.ProbabilityMeasure
